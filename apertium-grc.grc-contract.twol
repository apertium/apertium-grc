Alphabet
 Α Β Γ Δ Ε Ζ Η Θ Ι Κ Λ Μ Ν Ξ Ο Π Ρ Σ Τ Υ Φ Χ Ψ Ω
 α β γ δ ε ζ η θ ι κ λ μ ν ξ ο π ρ σ τ υ φ χ ψ ω
 ς
 ά έ ή ί ό ύ ώ
 ᾶ   ῆ ῖ   ῦ ῶ
 ᾳ ῃ ῳ
 ᾴ ῄ ῴ
 ᾷ ῇ ῷ
 %{%+%}:0
 ;

Sets

PreVowel = α ά ᾶ ε έ ο ό ;
PreVowelα = α ά ᾶ ;
PreVowelε = ε έ ;
PreVowelο = ο ό ;
PreVowelεο = ε έ ο ό ;

Rules

!!!!!!!!!!!!!!!
!!! First vowel
!!!!!!!!!!!!!!!

"α -> α{+long}"
ά:ᾶ <=> _ %{%+%}:0 [ α:0 | ε:0 | η:0 ] ;
        except _ %{%+%}:0 [ ε:0 | α:0 ] ι:0 ;

"α -> ᾳ"
Vx:Vy <=> _ %{%+%}:0 [ α:0 ι:0 | ε:0 ι:0 | ᾳ:0 | ῃ:0 ] ;
          where Vx in ( α ά ᾶ )
                Vy in ( ᾳ ᾷ ᾷ )
          matched ;

"α -> ω"
Vx:Vy <=> _ %{%+%}:0 [ ο:0 ( υ:0 ) | ω:0 ] ;
          except _ %{%+%}:0 ο:0 ι:0 ;
          where Vx in ( α ά ᾶ )
                Vy in ( ω ῶ ῶ )
          matched ;

"α -> ώ"
α:ώ <=> _ %{%+%}:0 [ ό:0 | ο:0 ύ:0 | ώ:0 ] ;

"α -> ῳ"
Vx:Vy <=> _ %{%+%}:0 [ ο:0 ι:0 | ῳ:0 ] ;
          where Vx in ( α ά ᾶ )
                Vy in ( ῳ ῷ ῷ )
          matched ;

"ε -> η"
Vx:Vy <=> _ %{%+%}:0 [ α:0 | η:0 ] ;
          except _ %{%+%}:0 α: ι: ;
          where Vx in ( ε έ )
                Vy in ( η ῆ )
          matched ;

"ε -> ῃ"
Vx:Vy <=> _ %{%+%}:0 [ α:0 ι:0 | ᾳ:0 | ῃ:0 ] ;
          where Vx in ( ε έ )
                Vy in ( ῃ ῇ )
          matched ;

"έ -> ε"
έ:ε <=> _ %{%+%}:0 [ ε: ( ι: ) | ο: ι: ] ;

"ε -> ου"
Vx:ο <=> _ %{%+%}:0 ο: ( υ: ) ;
         where Vx in ( ε έ ) ;

"ε -> ω"
Vx:Vy <=> _ %{%+%}:0 ω:0 ;
          where Vx in ( ε έ )
                Vy in ( ω ῶ )
          matched ;

!!!!!!!!!!!!!!!!
!!! Second vowel
!!!!!!!!!!!!!!!!

"following vowel"
Vx:0 <=> PreVowel: %{%+%}:0 _ ;
         where Vx in ( α ω ᾳ ῃ ῳ ) ;

"following ε"
ε:0 <=> PreVowelα: %{%+%}:0 _ ;
        PreVowelεο: %{%+%}:0 _ ι: ;

"following ο"
ο:0 <=> PreVowelα: %{%+%}:0 _ ;
        PreVowelεο: %{%+%}:0 _ υ: ;

"following ε -> ι"
ε:Vx <=> Vy: %{%+%}:0 _ ;
         except _ ι: ;
         where Vx in ( ι ῖ )
               Vy in ( ε έ )
         matched ;

"diphthong ι"
ι:0 <=> α: %{%+%}: ? _ ;

"ι -> ῖ"
ι:ῖ <=> [ έ: | ό: ] %{%+%}:0 ε: _ ;

"υ -> ῦ"
υ:ῦ <=> [ έ: | ό: ] %{%+%}:0 ο: _ ;
Alphabet
 Α Β Γ Δ Ε Ζ Η Θ Ι Κ Λ Μ Ν Ξ Ο Π Ρ Σ Τ Υ Φ Χ Ψ Ω
 α β γ δ ε ζ η θ ι κ λ μ ν ξ ο π ρ σ τ υ φ χ ψ ω
 ς
 ᾳ ῃ ῳ
 ϊ ϋ
 Ἀ Ἐ Ἠ Ἰ Ὀ   Ὠ  ᾘ ᾨ
 ἀ ἐ ἠ ἰ ὀ ὐ ὠ  ᾐ ᾠ
 Ἁ Ἑ Ἡ Ἱ Ὁ Ὑ Ὡ  ᾙ ᾩ
 ἁ ἑ ἡ ἱ ὁ ὑ ὡ  ᾑ ᾡ
 Ῥ ῥ

 ! pass these through to the stress twol file
 %{'%} %{'%!%} %{%~%} %{%+long%} %{%-long%} %{clt%}

 %{%+%}
 ;

Sets

Vowel = Α Ε Η Ι Ο Υ Ω
        α ε η ι ο υ ω
        ᾳ ῃ ῳ
        ϊ ϋ ;

Consonant = Β Γ Δ Ζ Θ Κ Λ Μ Ν Ξ Π Ρ Σ Τ Φ Χ Ψ
            β γ δ ζ θ κ λ μ ν ξ π ρ σ τ φ χ ψ
                                    ς ;

Mod = %{'%} %{clt%} ;

Rules

"initial vowel"
Vx:Vy <=> .#. ( Mod ) _ ;
          except _ [ :ἰ | :ὐ | :ἱ | :ὑ ] ;
          where Vx in ( Α Ε Η Ι Ο Ω α ε η ο ω ῃ ῳ )
                Vy in ( Ἀ Ἐ Ἠ Ἰ Ὀ Ὠ ἀ ἐ ἠ ὀ ὠ ᾐ ᾠ )
          matched ;

"ι"
ι:ἰ <=> .#. ( Mod ) ( α | ε | ο | υ | Α | Ε | Ο | Υ ) _ ;

"υ"
υ:ὐ <=> .#. ( Mod ) ( α | ε | η | ο | ω | Α | Ε | Η | Ο | Ω ) _ ;
        except _ [ :ἰ | :ἱ ] ;

"σ"
σ:ς <=> _ .#. ;

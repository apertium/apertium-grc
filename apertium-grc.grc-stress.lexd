PATTERNS
# final stress
Prefix FinalAcute Acute BD CC
Prefix LongVowel(3) Circumflex BD Consonant*
Acute CC FinalAcute BD CC

# penultimate stress
Acute (Consonant*) ShortVowel(2) BD UnmarkedSyllable (Consonant*)
Prefix CC AnyVowel Acute (BD) CC AnyAcuteVowel (BD) FinalLongSyllable
Prefix ShortVowel(2) Acute BD UnmarkedSyllable Consonant*
Prefix AnyAcuteVowel Acute BD FinalLongSyllable
Prefix LongVowel(3) (Acute | Circumflex) BD FinalShortSyllable
Prefix LongVowel(2) ForceAcute BD FinalShortSyllable
Acute Consonant* LongVowel(3) BD FinalShortSyllable
Acute Consonant* AnyAcuteVowel BD FinalLongSyllable
Acute IrrelevantSyllable* CC AnyAcuteVowel BD FinalLongSyllable

# antepenultimate stress
Prefix AnyAcuteVowel Acute BD UnmarkedSyllable FinalShortSyllable
Acute IrrelevantSyllable* CC AnyAcuteVowel BD UnmarkedSyllable FinalShortSyllable

# following clitic
# TODO: mark these somehow for disam & generation
Prefix LongVowel(3) (Acute | Circumflex) BD FinalShortSyllableClitic
Acute CC LongVowel(3) BD FinalShortSyllableClitic
Prefix AnyAcuteVowel Acute BD UnmarkedSyllable FinalShortSyllableClitic
Acute IrrelevantSyllable* CC AnyAcuteVowel BD UnmarkedSyllable FinalShortSyllableClitic
UnmarkedSyllable* CC AnyAcuteVowel BD CC [']?

# clitic
Clt UnmarkedSyllable CC
Clt CC FinalAcute BD CC
Clt UnmarkedSyllable UnmarkedSyllable CC
Clt UnmarkedSyllable CC FinalAcute BD CC
Clt UnmarkedSyllable CC LongVowel(3) BD CC

# no stress
UnmarkedSyllable* Consonant* [']?

# make sure this gets passed through
Punctuation

PATTERN AnyVowel
ShortVowel(1)
LongVowel(1)

PATTERN AnyAcuteVowel
ShortVowel(2)
LongVowel(2)

PATTERN AnyCircumflexVowel
LongVowel(3)

PATTERN FinalAcute
AnyAcuteVowel
ShortVowel(3)
LongVowel(4)

PATTERN IrrelevantSyllable
(Consonant*) AnyVowel (Acute | Circumflex)? BD

PATTERN UnmarkedSyllable
(Consonant*) AnyVowel BD

PATTERN MarkedAcuteSyllable
(Consonant*) AnyAcuteVowel Acute BD

PATTERN MarkedCircumflexPenult
(Consonant*) AnyCircumflexVowel (Acute | Circumflex) BD ShortSyllable

PATTERN MarkedCircumflexSyllable
(Consonant*) AnyCircumflexVowel Circumflex BD

PATTERN Prefix
Acute? IrrelevantSyllable* Consonant*

PATTERN FinalLongSyllable
CC LongVowel(1) BD CC

PATTERN FinalShortSyllable
CC ShortVowel(1) BD CC

PATTERN FinalShortSyllableClitic
CC ShortVowel(2) BD CC

LEXICON Acute
{'}:

LEXICON ForceAcute
{'!}:

LEXICON Circumflex
{~}:

LEXICON BD
{.}:

LEXICON Clt
{clt}:

PATTERN CC
Consonant*

LEXICON ShortVowel(3)
Α Α:Ά Α:Ὰ
Ε Ε:Έ Ε:Ὲ
Ι Ι:Ί Ι:Ὶ
Ο Ο:Ό Ο:Ὸ
Υ Υ:Ύ Υ:Ὺ
α α:ά α:ὰ
ε ε:έ ε:ὲ
ι ι:ί ι:ὶ
ο ο:ό ο:ὸ
υ υ:ύ υ:ὺ
Ἀ Ἀ:Ἄ Ἀ:Ἂ
Ἐ Ἐ:Ἔ Ἐ:Ἒ
Ἰ Ἰ:Ἴ Ἰ:Ἲ
Ὀ Ὀ:Ὄ Ὀ:Ὂ
ἀ ἀ:ἄ ἀ:ἂ
ἐ ἐ:ἔ ἐ:ἒ
ἰ ἰ:ἴ ἰ:ἲ
ὀ ὀ:ὄ ὀ:ὂ
ὐ ὐ:ὔ ὐ:ὒ
Ἁ Ἁ:Ἅ Ἁ:Ἃ
Ἑ Ἑ:Ἕ Ἑ:Ἓ
Ἱ Ἱ:Ἵ Ἱ:Ἳ
Ὁ Ὁ:Ὅ Ὁ:Ὃ
Ὑ Ὑ:Ὕ Ὑ:Ὓ
ἁ ἁ:ἅ ἁ:ἃ
ἑ ἑ:ἕ ἑ:ἓ
ἱ ἱ:ἵ ἱ:ἳ
ὁ ὁ:ὅ ὁ:ὃ
ὑ ὑ:ὕ ὑ:ὓ
αῐ:αι αῐ:αί αῐ:αὶ
οῐ:οι οῐ:οί οῐ:οὶ
ω{-long}:ω ω{-long}:ώ ω{-long}:ὼ
ϊ ϊ:ΐ ϊ:ῒ
ϋ ϋ:ΰ ϋ:ῢ

LEXICON LongVowel(4)
α{+long}:α α{+long}:ά α{+long}:ᾶ α{+long}:ὰ
η          η:ή        η:ῆ        η:ὴ
ι{+long}:ι ι{+long}:ί ι{+long}:ῖ ι{+long}:ὶ
υ{+long}:υ υ{+long}:ύ υ{+long}:ῦ υ{+long}:ὺ
ω          ω:ώ        ω:ῶ        ω:ὼ
Ἀ{+long}:Ἀ Ἀ{+long}:Ἄ Ἀ{+long}:Ἆ Ἀ{+long}:Ἂ
Ἠ          Ἠ:Ἤ        Ἠ:Ἦ        Ἠ:Ἢ
Ἰ{+long}:Ἰ Ἰ{+long}:Ἴ Ἰ{+long}:Ἶ Ἰ{+long}:Ἲ
Ὠ          Ὠ:Ὤ        Ὠ:Ὦ        Ὠ:Ὢ
ἀ{+long}:ἀ ἀ{+long}:ἄ ἀ{+long}:ἆ ἀ{+long}:ἂ
ἠ          ἠ:ἤ        ἠ:ἦ        ἠ:ἢ
ἰ{+long}:ἰ ἰ{+long}:ἴ ἰ{+long}:ἶ ἰ{+long}:ἲ
ὐ{+long}:ὐ ὐ{+long}:ὔ ὐ{+long}:ὖ ὐ{+long}:ὒ
ὠ          ὠ:ὤ        ὠ:ὦ        ὠ:ὢ
Ἁ{+long}:Ἁ Ἁ{+long}:Ἅ Ἁ{+long}:Ἇ Ἁ{+long}:Ἃ
Ἡ          Ἡ:Ἥ        Ἡ:Ἧ        Ἡ:Ἣ
Ἱ{+long}:Ἱ Ἱ{+long}:Ἵ Ἱ{+long}:Ἷ Ἱ{+long}:Ἳ
Ὑ{+long}:Ὑ Ὑ{+long}:Ὕ Ὑ{+long}:Ὗ Ὑ{+long}:Ὓ
Ὠ          Ὡ:Ὥ        Ὡ:Ὧ        Ὡ:Ὣ
ἁ{+long}:ἁ ἁ{+long}:ἅ ἁ{+long}:ἇ ἁ{+long}:ἃ
ἡ          ἡ:ἥ        ἡ:ἧ        ἡ:ἣ
ἱ{+long}:ἱ ἱ{+long}:ἵ ἱ{+long}:ἷ ἱ{+long}:ἳ
ὑ{+long}:ὑ ὑ{+long}:ὕ ὑ{+long}:ὗ ὑ{+long}:ὓ
ὡ          ὡ:ὥ        ὡ:ὧ        ὡ:ὣ
Αι         Αι:Αί      Αι:Αῖ      Αι:Αὶ
Αἰ         Αἰ:Αἴ      Αἰ:Αἶ      Αἰ:Αἲ
Αἱ         Αἱ:Αἵ      Αἱ:Αἷ      Αἱ:Αἳ
αι         αι:αί      αι:αῖ      αι:αὶ
αἰ         αἰ:αἴ      αἰ:αἶ      αἰ:αἲ
αἱ         αἱ:αἵ      αἱ:αἷ      αἱ:αἳ
Ει         Ει:Εί      Ει:Εῖ      Ει:Εὶ
Εἰ         Εἰ:Εἴ      Εἰ:Εἶ      Εἰ:Εἲ
Εἱ         Εἱ:Εἵ      Εἱ:Εἷ      Εἱ:Εἳ
ει         ει:εί      ει:εῖ      ει:εὶ
εἰ         εἰ:εἴ      εἰ:εἶ      εἰ:εἲ
εἱ         εἱ:εἵ      εἱ:εἷ      εἱ:εἳ
Οι         Οι:Οί      Οι:Οῖ      Οι:Οὶ
Οἰ         Οἰ:Οἴ      Οἰ:Οἶ      Οἰ:Οἲ
Οἱ         Οἱ:Οἵ      Οἱ:Οἷ      Οἱ:Οἳ
οι         οι:οί      οι:οῖ      οι:οὶ
οἰ         οἰ:οἴ      οἰ:οἶ      οἰ:οἲ
οἱ         οἱ:οἵ      οἱ:οἷ      οἱ:οἳ
Υι         Υι:Υί      Υι:Υῖ      Υι:Υὶ
Υἰ         Υἰ:Υἴ      Υἰ:Υἶ      Υἰ:Υἲ
Υἱ         Υἱ:Υἵ      Υἱ:Υἷ      Υἱ:Υἳ
υι         υι:υί      υι:υῖ      υι:υὶ
υἰ         υἰ:υἴ      υἰ:υἶ      υἰ:υἲ
υἱ         υἱ:υἵ      υἱ:υἷ      υἱ:υἳ
Αυ         Αυ:Αύ      Αυ:Αῦ      Αυ:Αὺ
Αὐ         Αὐ:Αὔ      Αὐ:Αὖ      Αὐ:Αὒ
Αὑ         Αὑ:Αὕ      Αὑ:Αὗ      Αὑ:Αὓ
αυ         αυ:αύ      αυ:αῦ      αυ:αὺ
αὐ         αὐ:αὔ      αὐ:αὖ      αὐ:αὒ
αὑ         αὑ:αὕ      αὑ:αὗ      αὑ:αὓ
Ευ         Ευ:Εύ      Ευ:Εῦ      Ευ:Εὺ
Εὐ         Εὐ:Εὔ      Εὐ:Εὖ      Εὐ:Εὒ
Εὑ         Εὑ:Εὕ      Εὑ:Εὗ      Εὑ:Εὓ
ευ         ευ:εύ      ευ:εῦ      ευ:εὺ
εὐ         εὐ:εὔ      εὐ:εὖ      εὐ:εὒ
εὑ         εὑ:εὕ      εὑ:εὗ      εὑ:εὓ
Ηυ         Ηυ:Ηύ      Ηυ:Ηῦ      Ηυ:Ηὺ
Ηὐ         Ηὐ:Ηὔ      Ηὐ:Ηὖ      Ηὐ:Ηὒ
Ηὑ         Ηὑ:Ηὕ      Ηὑ:Ηὗ      Ηὑ:Ηὓ
ηυ         ηυ:ηύ      ηυ:ηῦ      ηυ:ηὺ
ηὐ         ηὐ:ηὔ      ηὐ:ηὖ      ηὐ:ηὒ
ηὑ         ηὑ:ηὕ      ηὑ:ηὗ      ηὑ:ηὓ
Ου         Ου:Ού      Ου:Οῦ      Ου:Οὺ
Οὐ         Οὐ:Οὔ      Οὐ:Οὖ      Οὐ:Οὒ
Οὑ         Οὑ:Οὕ      Οὑ:Οὗ      Οὑ:Οὓ
ου         ου:ού      ου:οῦ      ου:οὺ
οὐ         οὐ:οὔ      οὐ:οὖ      οὐ:οὒ
οὑ         οὑ:οὕ      οὑ:οὗ      οὑ:οὓ
Ωυ         Ωυ:Ωύ      Ωυ:Ωῦ      Ωυ:Ωὺ
Ωὐ         Ωὐ:Ωὔ      Ωὐ:Ωὖ      Ωὐ:Ωὒ
Ωὑ         Ωὑ:Ωὕ      Ωὑ:Ωὗ      Ωὑ:Ωὓ
ωυ         ωυ:ωύ      ωυ:ωῦ      ωυ:ωὺ
ωὐ         ωὐ:ωὔ      ωὐ:ωὖ      ωὐ:ωὒ
ωὑ         ωὑ:ωὕ      ωὑ:ωὗ      ωὑ:ωὓ
# TODO: capital iota subscript?
ᾳ          ᾳ:ᾴ        ᾳ:ᾷ        ᾳ:ᾲ
ᾀ          ᾀ:ᾄ        ᾀ:ᾆ        ᾀ:ᾂ
ᾁ          ᾁ:ᾅ        ᾁ:ᾇ        ᾁ:ᾃ
ῃ          ῃ:ῄ        ῃ:ῇ        ῃ:ῂ
ᾐ          ᾐ:ᾔ        ᾐ:ᾖ        ᾐ:ᾒ
ᾑ          ᾑ:ᾕ        ᾑ:ᾗ        ᾑ:ᾓ
ῳ          ῳ:ῴ        ῳ:ῷ        ῳ:ῲ
ᾠ          ᾠ:ᾤ        ᾠ:ᾦ        ᾠ:ᾢ
ᾡ          ᾡ:ᾥ        ᾡ:ᾧ        ᾡ:ᾣ

LEXICON Consonant
Β
Γ
Δ
Ζ
Θ
Κ
Λ
Μ
Ν
Ξ
Π
Ρ
Ῥ
Σ
Τ
Φ
Χ
Ψ
β
γ
δ
ζ
θ
κ
λ
μ
ν
ξ
π
ρ
ῥ
σ
ς
τ
φ
χ
ψ
{+}

LEXICON Punctuation
,
.
;
·

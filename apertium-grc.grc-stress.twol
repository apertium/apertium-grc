Alphabet
 Α Β Γ Δ Ε Ζ Η Θ Ι Κ Λ Μ Ν Ξ Ο Π Ρ Σ Τ Υ Φ Χ Ψ Ω
 α β γ δ ε ζ η θ ι κ λ μ ν ξ ο π ρ σ τ υ φ χ ψ ω
 ς
 Ἀ Ἐ Ἠ Ἰ Ὀ   Ὠ
 ἀ ἐ ἠ ἰ ὀ ὐ ὠ
 Ἁ Ἑ Ἡ Ἱ Ὁ Ὑ Ὡ
 ἁ ἑ ἡ ἱ ὁ ὑ ὡ
 %{'%}:0 %{%~%}:0 %{%+long%}:0
 Α:Ά Ε:Έ Η:Ή Ι:Ί Ο:Ό Υ:Ύ Ω:Ώ
 α:ά ε:έ η:ή ι:ί ο:ό υ:ύ ω:ώ
 α:ᾶ η:ῆ ι:ῖ υ:ῦ ω:ῶ
 ῃ:ῄ ῃ:ῇ ῳ:ῴ ῳ:ῷ
 Ἀ:Ἄ Ἐ:Ἔ Ἠ:Ἤ Ἰ:Ἴ Ὀ:Ὄ     Ὠ:Ὤ
 ἀ:ἄ ἐ:ἔ ἠ:ἤ ἰ:ἴ ὀ:ὄ ὐ:ὔ ὠ:ὤ
 Ἁ:Ἅ Ἑ:Ἕ Ἡ:Ἥ Ἱ:Ἵ Ὁ:Ὅ Ὑ:Ὅ Ὡ:Ὥ
 ἁ:ἅ ἑ:ἕ ἡ:ἥ ἱ:ἵ ὁ:ὅ ὑ:ὕ ὡ:ὥ
 ;

Sets

Vowel = Α Ε Η Ι Ο Υ Ω
        α ε η ι ο υ ω
        ῃ ῳ ;

ShortVowel = Α Ε Ι Ο Υ
             α ε ι ο υ ;

SingleLongVowel = Η Ω
                  η ω
                  ῃ ῳ ;

Consonant = Β Γ Δ Ζ Θ Κ Λ Μ Ν Ξ Π Ρ Σ Τ Φ Χ Ψ
            β γ δ ζ θ κ λ μ ν ξ π ρ σ τ φ χ ψ
                                    ς ;

ShortUnmarked = Α Ε Ι Ο Υ α ε ι ο υ Ἀ Ἐ Ἰ Ὀ ἀ ἐ ἰ ὀ ὐ Ἁ Ἑ Ἱ Ὁ Ὑ ἁ ἑ ἱ ὁ ὑ ;
ShortAcute    = Ά Έ Ί Ό Ύ ά έ ί ό ύ Ἄ Ἔ Ἴ Ὄ ἄ ἔ ἴ ὄ ὔ Ἅ Ἕ Ἵ Ὅ Ὕ ἅ ἕ ἵ ὅ ὕ ;

Definitions

LongVowel = [ SingleLongVowel | ShortVowel %{%+long%}:0 ] ;

ShortFinal = [ ShortVowel Consonant .#. | ShortVowel .#. | α ι .#. | ο ι .#. ] ;

Rules

"marked short final acute"
Vx:Vy <=> _ %{'%}:0 Consonant* .#. ;
    where Vx in ShortUnmarked
          Vy in ShortAcute
          matched ;

"marked short penult acute"
Vx:Vy <=> _ %{'%}:0 Consonant* Vowel+ Consonant* .#. ;
    where Vx in ShortUnmarked
          Vy in ShortAcute
          matched ;

"marked long final acute"
Vx:Vy <=> _ %{'%}:0 Consonant* .#. ;
    where Vx in ( η ω ῃ ῳ )
          Vy in ( ή ώ ῄ ῴ )
          matched;

"marked lengthened final acute"
Vx:Vy <=> _ %{%+long%}:0 %{'%}:0 Consonant* .#. ;
    where Vx in ( α ι υ ἀ ἰ ὐ ἁ ἱ ὑ )
          Vy in ( ά ί ύ ἄ ἴ ὔ ἅ ἵ ὕ )
          matched;

"marked long final circumflex"
Vx:Vy <=> _ %{%~%}:0 Consonant* .#. ;
    where Vx in ( η ω ῃ ῳ )
          Vy in ( ῆ ῶ ῇ ῷ )
          matched;

"lengthened final circumflex"
Vx:Vy <=> _ %{%+long%}:0 %{%~%}:0 Consonant* .#. ;
    where Vx in ( α ι υ )
          Vy in ( ᾶ ῖ ῦ )
          matched;

"diphthong final circumflex"
Vx:Vy <=> Vowel _ %{%~%}:0 Consonant* .#. ;
    where Vx in ( ι υ ἰ ὐ ἱ ὑ )
          Vy in ( ῖ ῦ ἶ ὖ ἷ ὗ )
          matched;

"diphthong penult circumflex"
Vx:Vy <=> Vowel _ %{%~%}:0 Consonant+ ShortFinal ;
    where Vx in ( ι υ ἰ ὐ ἱ ὑ )
          Vy in ( ῖ ῦ ἶ ὖ ἷ ὗ )
          matched;

"antepenult short unmarked"
Vx:Vy <=> %{'%}:0 ?* _ Consonant+ Vowel Consonant+ ShortFinal ;
    where Vx in ShortUnmarked
          Vy in ShortAcute
          matched ;

"penult acute unmarked"
Vx:Vy <=> %{'%}:0 ?* _ Consonant+ LongVowel Consonant* .#. ;
          %{'%}:0 Consonant* _ Consonant+ Vowel+ Consonant* .#. ;
    where Vx in ShortUnmarked
          Vy in ShortAcute
          matched ;

! Resources:
! http://wiki.apertium.org/wiki/Starting_a_new_language_with_HFST#Enter_twol
! https://kitwiki.csc.fi/twiki/bin/view/KitWiki/HfstHome
! https://github.com/hfst/
! http://wiki.apertium.org/wiki/Twol
! http://wiki.apertium.org/wiki/Hfst


# Morphological Transducer for Ancient Greek

# {ε} verb past tense augment
# {+long} lengthening
# {σ} disappearing sigma
# {(ν)} movable nu
# {'} stress location (assign stress to vowel preceding last of these)
# {~} stress location, circumflex if possible

##################################################
# NOUNS
##################################################

PATTERNS
NOUN
VERB
Determiners
Conjunctions
Prepositions
PersonalPronouns
Negation
Punctuation

PATTERN NOUN
NounStem[ᾱ,ᾱς,η] [<n><f>:] CaseNumber[ᾱ,ᾱς,η]
NounStem[ος,ου,η] [<n><f>:] CaseNumber[ος,ου,η]
NounStem[ος,ου,ο] [<n><m>:] CaseNumber[ος,ου,ο]

ProperNouns[indec,sg] CaseNumber[indec,sg]

LEXICON CaseNumber
<nom><sg>:ᾱ[ᾱ,ᾱς,η]
<gen><sg>:ᾱς[ᾱ,ᾱς,η]

<nom><sg>:ος[ος,ου,η]

<gen><sg>:ου{~}[ος,ου,ο] # TODO: is this right for stress?

<nom><sg>:[indec,sg]
<gen><sg>:[indec,sg]
<dat><sg>:[indec,sg]
<acc><sg>:[indec,sg]
<voc><sg>:[indec,sg]

LEXICON NounStem
βιβλος:βι{'}βλ[ος,ου,η]
θεᾱ:θε[ᾱ,ᾱς,η]
θεος:θε[ος,ου,ο]

LEXICON ProperNouns
Δαβιδ<np><ant><m>:Δαβι{'}δ[indec,sg]

##################################################
# Ω VERBS
##################################################

PATTERN VERB
VerbStem1 [<v><ind><actv><impf><pres>:] (ThematicVowelInd VerbPrimary1)[^[1sg,2sg,3sg,1pl,2pl,3pl]]
VerbStem2 [<v><ind><actv><impf><fut>:] (ThematicVowelInd VerbPrimary1)[^[1sg,2sg,3sg,1pl,2pl,3pl]] # CHECK aspect
VerbStemAug1 [<v><ind><actv><impf><past>:] (ThematicVowelInd VerbSecondary1)[^[1sg,2sg,3sg,1pl,2pl,3pl]]
VerbStem1 [<v><ind>:] MP [<impf><pres>:] (ThematicVowelInd VerbPrimary2)[^[1sg,2sg,3sg,1pl,2pl,3pl]]
VerbStem2 [<v><ind><midv><impf><fut>:] (ThematicVowelInd VerbPrimary2)[^[1sg,2sg,3sg,1pl,2pl,3pl]] # CHECK aspect
VerbStemAug1 [<v><ind>:] MP [<impf><past>:] (ThematicVowelInd VerbPrimary2)[^[1sg,2sg,3sg,1pl,2pl,3pl]]

VerbStem1 [<v><ind><actv><impf><inf>:ειν]
VerbStem1 [<v><ind><pass><impf><inf>:εσθαι]
VerbStem2 [<v><ind><actv><impf><inf>:ειν]
VerbStem2 [<v><ind><midv><impf><inf>:εσθαι]

LEXICON MP
<midv>:
<pass>:

PATTERN VerbStem1
VerbRootRegular

PATTERN VerbStemAug1
[:{ε}] VerbRootRegular

PATTERN VerbStem2
VerbRootRegular [:σ]

LEXICON VerbPrimary1
[1sg]<p1><sg>:{+long}
[2sg]<p2><sg>:{σ}ισ
[3sg]<p3><sg>:{σ}ι
[1pl]<p1><pl>:μεν
[2pl]<p2><pl>:τε
[3pl]<p3><pl>:{+long}σι{(ν)}

LEXICON VerbImperative1
[2sgi]<p2><sg>:
[3sgi]<p3><sg>:τω
[2pli]<p2><pl>:τε
[3pli]<p3><pl>:ντων

LEXICON VerbSecondary1
[1sg]<p1><sg>:μ
[2sg]<p2><sg>:σ
[3sg]<p3><sg>:
[1pl]<p1><pl>:μεν
[2pl]<p2><pl>:τε
[3pl]<p3><pl>:ντ # τ always drops?

LEXICON VerbPrimary2
[1sg]<p1><sg>:μαι
[2sg]<p2><sg>:{σ}αι
[3sg]<p3><sg>:ται
[1pl]<p1><pl>:μεθα
[2pl]<p2><pl>:σθε
[3pl]<p3><pl>:νται

LEXICON VerbSecondary2
[1sg]<p1><sg>:μην
[2sg]<p2><sg>:{σ}ο
[3sg]<p3><sg>:το
[1pl]<p1><pl>:μεθα
[2pl]<p2><pl>:σθε
[3pl]<p3><pl>:ντο

LEXICON ThematicVowelInd
:ο[1sg]
:ο[1pl]
:ο[3pl]
:ε[2sg]
:ε[3sg]
:ε[2pl]

LEXICON ThematicVowelImp
:ο[3pli]
:ε[2sgi]
:ε[2pli]
:ε[3sgi]

LEXICON VerbRootRegular
παιδευω:παιδευ

##################################################
# ΜΙ VERBS
##################################################

LEXICON MILengthening
:{+long}[1sg]
:{+long}[2sg]
:{+long}[3sg]
:[1pl]
:[2pl]
:[3pl]

LEXICON MIEndings(6)
# 1 = pres act ind
# 2 = impf act ind, 2nd aor act ind
# 3 = 1st aor act ind
# 4 = pres mid/pass ind
# 5 = impf mid/pass ind
[1sg]<p1><sg>:μι      :ον  :κα      :μαι  :μην  :μην
[2sg]<p2><sg>:σ       :εσ  :κασ     :σαι  :σο   :{σ}ο
[3sg]<p3><sg>:σι{(ν)} :ε   :κε{(ν)} :ται  :το   :το
[1pl]<p1><pl>:μεν     :μεν :μεν     :μεθα :μεθα :μεθα
[2pl]<p2><pl>:τε      :τε  :τε      :σθε  :σθε  :σθε
[3pl]<p3><pl>:σι{(ν)} :σαν :σαν     :νται :ντο  :ντο

PATTERNS
[:{redup}ι] MIVerbRoot [<v><ind><actv><impf><pres>:] (MILengthening MIEndings(1))[^[1sg,2sg,3sg,1pl,2pl,3pl]]
[:{ε}{redup}ι] MIVerbRoot [<v><ind><actv><impf><past>:] (MILengthening MIEndings(1): :MIEndings(2))[^[1sg,2sg,3sg,1pl,2pl,3pl]]
[:{ε}] MIVerbRoot[1aor] [<v><ind><actv><aor><past>:] (MILengthening MIEndings(1): :MIEndings(3))[^[1sg,2sg,3sg,1pl,2pl,3pl]]
[:{ε}] MIVerbRoot[2aor] [<v><ind><actv><aor><past>:{+long}] (MIEndings(1): :MIEndings(2))[^[1sg,2sg,3sg,1pl,2pl,3pl]]
[:{redup}ι] MIVerbRoot [<v><ind>:] MP [<impf><pres>:] VerbPrimary2
[:{redup}ι] MIVerbRoot [<v><ind>:] MP [<impf><past>:] (MIEndings(1): :MIEndings(4))[^[1sg,2sg,3sg,1pl,2pl,3pl]]
[:{ε}] MIVerbRoot[1aor] [<v><ind><midv><aor><past>:] VerbSecondary2
[:{redup}ι] MIVerbRoot [<v><sub><actv><impf><pres>:{+long}] (ThematicVowelInd VerbPrimary1)[^[1sg,2sg,3sg,1pl,2pl,3pl]]
MIVerbRoot [<v><sub><actv><aor><past>:{+long}] (ThematicVowelInd VerbPrimary1)[^[1sg,2sg,3sg,1pl,2pl,3pl]]
[:{redup}ι] MIVerbRoot [<v><sub>:] MP [<impf><pres>:{+long}] (ThematicVowelInd VerbPrimary2)[^[1sg,2sg,3sg,1pl,2pl,3pl]]

LEXICON MIVerbRoot
διδωμι:δο[1aor]
ιστημι:στα[2aor]

LEXICON Determiners
τ<det><def><m><sg><nom>:ὁ
τ<det><def><m><sg><gen>:του{~}
τ<det><def><m><sg><dat>:τῳ{~}
τ<det><def><m><sg><acc>:το{'}ν
τ<det><def><m><pl><nom>:οἱ
τ<det><def><m><pl><gen>:τω{~}ν
τ<det><def><m><pl><dat>:τοι{~}ς
τ<det><def><m><pl><acc>:του{'}ς

τ<det><def><f><sg><nom>:ἡ
τ<det><def><f><sg><gen>:τη{~}ς
τ<det><def><f><sg><dat>:τῃ{~}
τ<det><def><f><sg><acc>:τη{'}ν
τ<det><def><f><pl><nom>:αἱ
τ<det><def><f><pl><gen>:τω{~}ν
τ<det><def><f><pl><dat>:ται{~}ς
τ<det><def><f><pl><acc>:τα{'}ς # TODO: long

τ<det><def><nt><sg><nom>:το{'}
τ<det><def><nt><sg><gen>:του{~}
τ<det><def><nt><sg><dat>:τῳ{~}
τ<det><def><nt><sg><acc>:το{'}
τ<det><def><nt><pl><nom>:τα{'} # TODO: long
τ<det><def><nt><pl><gen>:τω{~}ν
τ<det><def><nt><pl><dat>:τοι{~}ς
τ<det><def><nt><pl><acc>:τα{'} # TOOD: long

LEXICON Conjunctions
αλλα<cnjcoo>:ἀλλα{'}
δε<cnjcoo>:δε{'}
γαρ<cnjcoo>:γα{'}ρ
και<cnjcoo>:καὶ # TODO: handle grave properly

LEXICON Prepositions # page 383
# TODO: abbreviated forms
αμφι<pr>:ἀμφι{'}
ανα<pr>:ἀνα{'}
αντι<pr>:ἀντι{'}
απο<pr>:ἀπο{'}
δια<pr>:δια{'}
εις<pr>:εἰς
εκ<pr>:ἐκ
εν<pr>:ἐν
επι<pr>:ἐπι{'}
κατα<pr>:κατα{'}
μετα<pr>:μετα{'}
παρα<pr>:παρα{'}
περι<pr>:περι{'}
προ<pr>:προ{'}
προς<pr>:προ{'}ς
συν<pr>:συ{'}ν
υπερ<pr>:ὑπε{'}ρ
υπο<pr>:ὑπο{'}

LEXICON PersonalPronouns
μο<prn><pers><p1><sg><nom>:ἐγω{'}
μο<prn><pers><p1><sg><gen>:ἐμου{~}
μο<prn><pers><p1><sg><gen>:μου
μο<prn><pers><p1><sg><dat>:ἐμοι{'}
μο<prn><pers><p1><sg><dat>:μοι
μο<prn><pers><p1><sg><acc>:ἐμε{'}
μο<prn><pers><p1><sg><acc>:με

ημε<prn><pers><p1><pl><nom>:ἡμει{~}ς
ημε<prn><pers><p1><pl><gen>:ἡμω{~}ν
ημε<prn><pers><p1><pl><dat>:ἡμι{~}ν # TODO: length
ημε<prn><pers><p1><pl><acc>:ἡμα{~}ς # TODO: length

σο<prn><pers><p2><sg><nom>:συ{'}
σο<prn><pers><p2><sg><gen>:σου{~}
σο<prn><pers><p2><sg><gen>:σου
σο<prn><pers><p2><sg><dat>:σοι{'}
σο<prn><pers><p2><sg><dat>:σοι
σο<prn><pers><p2><sg><acc>:σε{'}
σο<prn><pers><p2><sg><acc>:σε

# TODO: long υ
υμε<prn><pers><p2><pl><nom>:ὐμει{~}ς
υμε<prn><pers><p2><pl><gen>:ὐμω{~}ν
υμε<prn><pers><p2><pl><dat>:ὐμι{~}ν # TODO: length
υμε<prn><pers><p2><pl><acc>:ὐμα{~}ς # TODO: length

LEXICON Negation
μη<adv>:μη{'}
ουκ<adv>:οὐ
ουκ<adv>:οὐκ
ουκ<adv>:οὐχ

LEXICON Punctuation
,<cm>:,
.<sent>:.
;<sent>:;

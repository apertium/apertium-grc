# Morphological Transducer for Ancient Greek

# {ε} verb past tense augment
# {+long} lengthening
# {σ} disappearing sigma
# {(ν)} movable nu
# {'} stress location (assign stress to vowel preceding last of these)
# {~} stress location, circumflex if possible

##################################################
# NOUNS
##################################################

# TODO: organize

PATTERNS
NOUN
VERB
Adjectives
Copula
Determiners
RelativePronoun
Conjunctions
Prepositions
PersonalPronouns
Negation
Demonstratives
Punctuation

PATTERN NOUN
NounStem[ᾱ,ᾱς,η] [<n><f>:] CaseNumber[ᾱ,ᾱς,η]
NounStem[ος,ου,η] [<n><f>:] CaseNumber[ος,ου,η]
NounStem[ος,ου,ο,'$] [<n><m>:] CaseNumber[ος,ου,ο,'$]
NounStem[ος,ου,ο,-'$] [<n><m>:] CaseNumber[ος,ου,ο,-'$]
NounStem[η,ης,η,'$] [<n><f>:] CaseNumber[η,ης,η,'$]
NounStem[ο,ου,το,'$] [<n><nt>:] CaseNumber[ο,ου,το,'$]

ProperNouns[indec,sg] CaseNumber[indec,sg]
ProperNouns[ος,ου,ο,'$,sg] CaseNumber[ος,ου,ο,'$,sg]

IrregProperNouns

LEXICON CaseNumber
<nom><sg>:ᾱ[ᾱ,ᾱς,η]
<gen><sg>:ᾱς[ᾱ,ᾱς,η]

<nom><sg>:ος[ος,ου,η]

<nom><sg>:ο{'}ς[ος,ου,ο,'$,sg]
<gen><sg>:ου{~}[ος,ου,ο,'$,sg]
<dat><sg>:ῳ{~}[ος,ου,ο,'$,sg]
<acc><sg>:ο{'}ν[ος,ου,ο,'$,sg]

<nom><pl>:οι{'}[ος,ου,ο,'$]
<gen><pl>:ω{~}ν[ος,ου,ο,'$]
<dat><pl>:οι{~}ς[ος,ου,ο,'$]
<acc><pl>:ου{'}ς[ος,ου,ο,'$]

<nom><sg>:ος[ος,ου,ο]
<gen><sg>:ου[ος,ου,ο]
<dat><sg>:ῳ[ος,ου,ο]
<acc><sg>:ον[ος,ου,ο]

<nom><pl>:οι[ος,ου,ο]
<gen><pl>:ων[ος,ου,ο]
<dat><pl>:οι[ος,ου,ο]
<acc><pl>:ους[ος,ου,ο]

<nom><sg>:η{'}[η,ης,η,'$]
<gen><sg>:η{~}ς[η,ης,η,'$]
<dat><sg>:ῃ{~}[η,ης,η,'$]
<acc><sg>:η{'}ν[η,ης,η,'$]
#<voc><sg>:[η,ης,η,'$]

<nom><pl>:αι{'}[η,ης,η,'$]
<gen><pl>:ω{~}ν[η,ης,η,'$]
<dat><pl>:αι{~}ς[η,ης,η,'$]
<acc><pl>:α{+long}{'}ς[η,ης,η,'$]
#<voc><pl>:[η,ης,η,'$]

<nom><sg>:ο{'}[ο,ου,το,'$]
<gen><sg>:ου{~}[ο,ου,το,'$]
<dat><sg>:ῳ{~}[ο,ου,το,'$]
<acc><sg>:ο{'}[ο,ου,το,'$]
#<voc><sg>:[ο,ου,το,'$]

<nom><pl>:α{'}[ο,ου,το,'$]
<gen><pl>:ω{~}ν[ο,ου,το,'$]
<dat><pl>:οι{~}ς[ο,ου,το,'$]
<acc><pl>:α{'}[ο,ου,το,'$]
#<voc><pl>:[ο,ου,το,'$]

<nom><sg>:[indec,sg]
<gen><sg>:[indec,sg]
<dat><sg>:[indec,sg]
<acc><sg>:[indec,sg]
<voc><sg>:[indec,sg]

LEXICON NounStem
αυτος:αὐτ[ος,ου,ο,'$]
αυτος:αὐτ[η,ης,η,'$]
αυτος:αὐτ[ο,ου,το,'$]
βιβλος:βι{'}βλ[ος,ου,η]
κυριος:κυρι{'}[ος,ου,ο] # TODO: check accent
θεᾱ:θε[ᾱ,ᾱς,η]
θεος:θε[ος,ου,ο,'$]

LEXICON ProperNouns
Δαβιδ<np><ant><m>:Δαβι{'}δ[indec,sg]
Χριστος<np><ant><m>:Χριστ[ος,ου,ο,'$,sg] # TODO: check tags

LEXICON IrregProperNouns
Ιησους<np><ant><m><sg><nom>:Ἰησου{~}ς
Ιησους<np><ant><m><sg><gen>:Ἰησου{~}
Ιησους<np><ant><m><sg><dat>:Ἰησου{~}
Ιησους<np><ant><m><sg><acc>:Ἰησου{~}ν
Ιησους<np><ant><m><sg><voc>:Ἰησου{~}

PATTERN Adjectives
[πας<adj><m>:πα{'}ντ] AdjEndings(1): :AdjEndings(2)
[πας<adj><f>:πα{+long}{'}σ] AdjEndings(1): :AdjEndings(3)
[πας<adj><nt>:πα{'}ντ] AdjEndings(1): :AdjEndings(4)

LEXICON AdjEndings(4)
<nom><sg> ς     α  :
<gen><sg> ο{'}ς ης ο{'}ς
<dat><sg> ι{'}  ῃ  ι{'}
<acc><sg> α     αν :

<nom><pl> ες      αι        α
<gen><pl> ων      ω{~}ν     ων
<dat><pl> σι{(ν)} αις       σι{(ν)}
<acc><pl> ας      α{+long}ς α

##################################################
# Ω VERBS
##################################################

PATTERN VERB
[:{'}] VerbStem1 [<v><ind><actv><impf><pres>:] (ThematicVowelInd VerbPrimary1)[^[1sg,2sg,3sg,1pl,2pl,3pl]]
[:{'}] VerbStem2 [<v><ind><actv><impf><fut>:] (ThematicVowelInd VerbPrimary1)[^[1sg,2sg,3sg,1pl,2pl,3pl]] # CHECK aspect
[:{'}] VerbStemAug1 [<v><ind><actv><impf><past>:] (ThematicVowelInd VerbSecondary1)[^[1sg,2sg,3sg,1pl,2pl,3pl]]
[:{'}] VerbStem1 [<v><ind>:] MP [<impf><pres>:] (ThematicVowelInd VerbPrimary2)[^[1sg,2sg,3sg,1pl,2pl,3pl]]
[:{'}] VerbStem2 [<v><ind><midv><impf><fut>:] (ThematicVowelInd VerbPrimary2)[^[1sg,2sg,3sg,1pl,2pl,3pl]] # CHECK aspect
[:{'}] VerbStemAug1 [<v><ind>:] MP [<impf><past>:] (ThematicVowelInd VerbPrimary2)[^[1sg,2sg,3sg,1pl,2pl,3pl]]

VerbStem1 [<v><ind><actv><impf><inf>:ειν]
VerbStem1 [<v><ind><pass><impf><inf>:εσθαι]
VerbStem2 [<v><ind><actv><impf><inf>:ειν]
VerbStem2 [<v><ind><midv><impf><inf>:εσθαι]

LEXICON MP
<midv>:
<pass>:

PATTERN VerbStem1
VerbRootRegular

PATTERN VerbStemAug1
[:{ε}] VerbRootRegular

PATTERN VerbStem2
VerbRootRegular [:σ]

LEXICON VerbPrimary1
[1sg]<p1><sg>:{+long}
[2sg]<p2><sg>:{σ}ισ
[3sg]<p3><sg>:{σ}ι
[1pl]<p1><pl>:μεν
[2pl]<p2><pl>:τε
[3pl]<p3><pl>:{+long}σι{(ν)}

LEXICON VerbImperative1
[2sgi]<p2><sg>:
[3sgi]<p3><sg>:τω
[2pli]<p2><pl>:τε
[3pli]<p3><pl>:ντων

LEXICON VerbSecondary1
[1sg]<p1><sg>:μ
[2sg]<p2><sg>:σ
[3sg]<p3><sg>:
[1pl]<p1><pl>:μεν
[2pl]<p2><pl>:τε
[3pl]<p3><pl>:ντ # τ always drops?

LEXICON VerbPrimary2
[1sg]<p1><sg>:μαι
[2sg]<p2><sg>:{σ}αι
[3sg]<p3><sg>:ται
[1pl]<p1><pl>:μεθα
[2pl]<p2><pl>:σθε
[3pl]<p3><pl>:νται

LEXICON VerbSecondary2
[1sg]<p1><sg>:μην
[2sg]<p2><sg>:{σ}ο
[3sg]<p3><sg>:το
[1pl]<p1><pl>:μεθα
[2pl]<p2><pl>:σθε
[3pl]<p3><pl>:ντο

LEXICON ThematicVowelInd
:ο[1sg]
:ο[1pl]
:ο[3pl]
:ε[2sg]
:ε[3sg]
:ε[2pl]

LEXICON ThematicVowelImp
:ο[3pli]
:ε[2sgi]
:ε[2pli]
:ε[3sgi]

LEXICON VerbRootRegular
λεγω:λεγ
παιδευω:παιδευ

##################################################
# ΜΙ VERBS
##################################################

LEXICON MILengthening
:{+long}[1sg]
:{+long}[2sg]
:{+long}[3sg]
:[1pl]
:[2pl]
:[3pl]

LEXICON MIEndings(6)
# 1 = pres act ind
# 2 = impf act ind, 2nd aor act ind
# 3 = 1st aor act ind
# 4 = pres mid/pass ind
# 5 = impf mid/pass ind
[1sg]<p1><sg>:μι      :ον  :κα      :μαι  :μην  :μην
[2sg]<p2><sg>:σ       :εσ  :κασ     :σαι  :σο   :{σ}ο
[3sg]<p3><sg>:σι{(ν)} :ε   :κε{(ν)} :ται  :το   :το
[1pl]<p1><pl>:μεν     :μεν :μεν     :μεθα :μεθα :μεθα
[2pl]<p2><pl>:τε      :τε  :τε      :σθε  :σθε  :σθε
[3pl]<p3><pl>:σι{(ν)} :σαν :σαν     :νται :ντο  :ντο

PATTERNS
[:{'}{redup}ι] MIVerbRoot [<v><ind><actv><impf><pres>:] (MILengthening MIEndings(1))[^[1sg,2sg,3sg,1pl,2pl,3pl]]
[:{'}{ε}{redup}ι] MIVerbRoot [<v><ind><actv><impf><past>:] (MILengthening MIEndings(1): :MIEndings(2))[^[1sg,2sg,3sg,1pl,2pl,3pl]]
[:{'}{ε}] MIVerbRoot[1aor] [<v><ind><actv><aor><past>:] (MILengthening MIEndings(1): :MIEndings(3))[^[1sg,2sg,3sg,1pl,2pl,3pl]]
[:{'}{ε}] MIVerbRoot[2aor] [<v><ind><actv><aor><past>:{+long}] (MIEndings(1): :MIEndings(2))[^[1sg,2sg,3sg,1pl,2pl,3pl]]
[:{'}{redup}ι] MIVerbRoot [<v><ind>:] MP [<impf><pres>:] VerbPrimary2
[:{'}{redup}ι] MIVerbRoot [<v><ind>:] MP [<impf><past>:] (MIEndings(1): :MIEndings(4))[^[1sg,2sg,3sg,1pl,2pl,3pl]]
[:{'}{ε}] MIVerbRoot[1aor] [<v><ind><midv><aor><past>:] VerbSecondary2
[:{'}{redup}ι] MIVerbRoot [<v><sub><actv><impf><pres>:{+long}] (ThematicVowelInd VerbPrimary1)[^[1sg,2sg,3sg,1pl,2pl,3pl]]
[:{'}] MIVerbRoot [<v><sub><actv><aor><past>:{+long}] (ThematicVowelInd VerbPrimary1)[^[1sg,2sg,3sg,1pl,2pl,3pl]]
[:{'}{redup}ι] MIVerbRoot [<v><sub>:] MP [<impf><pres>:{+long}] (ThematicVowelInd VerbPrimary2)[^[1sg,2sg,3sg,1pl,2pl,3pl]]

LEXICON MIVerbRoot
διδωμι:δο[1aor]
ιστημι:στα[2aor]

LEXICON Copula
ειμι<vbser><ind><actv><impf><pres><p3><sg>:ἐστιν

LEXICON Determiners
τ<det><def><m><sg><nom>:ὁ
τ<det><def><m><sg><gen>:του{~}
τ<det><def><m><sg><dat>:τῳ{~}
τ<det><def><m><sg><acc>:το{'}ν
τ<det><def><m><pl><nom>:οἱ
τ<det><def><m><pl><gen>:τω{~}ν
τ<det><def><m><pl><dat>:τοι{~}ς
τ<det><def><m><pl><acc>:του{'}ς

τ<det><def><f><sg><nom>:ἡ
τ<det><def><f><sg><gen>:τη{~}ς
τ<det><def><f><sg><dat>:τῃ{~}
τ<det><def><f><sg><acc>:τη{'}ν
τ<det><def><f><pl><nom>:αἱ
τ<det><def><f><pl><gen>:τω{~}ν
τ<det><def><f><pl><dat>:ται{~}ς
τ<det><def><f><pl><acc>:τα{'}ς # TODO: long

τ<det><def><nt><sg><nom>:το{'}
τ<det><def><nt><sg><gen>:του{~}
τ<det><def><nt><sg><dat>:τῳ{~}
τ<det><def><nt><sg><acc>:το{'}
τ<det><def><nt><pl><nom>:τα{'} # TODO: long
τ<det><def><nt><pl><gen>:τω{~}ν
τ<det><def><nt><pl><dat>:τοι{~}ς
τ<det><def><nt><pl><acc>:τα{'} # TOOD: long

LEXICON RelativePronoun
ο<prn><rel><p3><m><sg><nom>:ὁ{'}ς
ο<prn><rel><p3><m><sg><acc>:ὁ{'}ν

η<prn><rel><p3><f><sg><nom>:ἡ{'}
η<prn><rel><p3><f><sg><acc>:ἡ{'}ν

ο<prn><rel><p3><nt><sg><nom>:ὁ{'}
ο<prn><rel><p3><nt><sg><acc>:ὁ{'}

LEXICON Conjunctions
αλλα<cnjcoo>:ἀλλα{'}
αλλα<cnjcoo>:ἀλλ # dir/LR TODO: apostrophe?
αν<cnjcoo>:ἀ{'}ν # TODO: check tag
δε<cnjcoo>:δε{'}
ει<cnjcoo>:εἰ # TODO: check tag
γαρ<cnjcoo>:γα{'}ρ
η<cnjcoo>:ἠ{'}
ινα<cnjsub>:ἱ{'}να
και<cnjcoo>:καὶ # TODO: handle grave properly
μεν<cnjcoo>:με{'}ν # TODO: check tag
οτι<cnjsub>:ὁ{'}τι
ουν<cnjcoo>:οὐ{~}ν  # TODO: check tag
τε<cnjcoo>:τε
ως<cnjsub>:ὡς

ει<cnjcoo>+αν<cnjcoo>:ἐα{+long}{'}ν
ει<cnjcoo>+αν<cnjcoo>:ἠ{'}ν
ει<cnjcoo>+αν<cnjcoo>:ἀ{+long}{'}ν

LEXICON Prepositions # page 383
# TODO: abbreviated forms
αμφι<pr>:ἀμφι{'}
ανα<pr>:ἀνα{'}
αντι<pr>:ἀντι{'}
απο<pr>:ἀπο{'}
δια<pr>:δια{'}
εις<pr>:εἰς
εκ<pr>:ἐκ
εκ<pr>:ἐχ
εκ<pr>:ἐξ # dir/LR
εν<pr>:ἐν # dir/LR
επι<pr>:ἐπι{'}
κατα<pr>:κατα{'}
μετα<pr>:μετα{'}
παρα<pr>:παρα{'}
περι<pr>:περι{'}
προ<pr>:προ{'}
προς<pr>:προ{'}ς
συν<pr>:συ{'}ν
υπερ<pr>:ὑπε{'}ρ
υπο<pr>:ὑπο{'}

LEXICON PersonalPronouns
μο<prn><pers><p1><sg><nom>:ἐγω{'}
μο<prn><pers><p1><sg><gen>:ἐμου{~}
μο<prn><pers><p1><sg><gen>:μου
μο<prn><pers><p1><sg><dat>:ἐμοι{'}
μο<prn><pers><p1><sg><dat>:μοι
μο<prn><pers><p1><sg><acc>:ἐμε{'}
μο<prn><pers><p1><sg><acc>:με

ημε<prn><pers><p1><pl><nom>:ἡμει{~}ς
ημε<prn><pers><p1><pl><gen>:ἡμω{~}ν
ημε<prn><pers><p1><pl><dat>:ἡμι{+long}{~}ν
ημε<prn><pers><p1><pl><acc>:ἡμα{+long}{~}ς

σο<prn><pers><p2><sg><nom>:συ{'}
σο<prn><pers><p2><sg><gen>:σου{~}
σο<prn><pers><p2><sg><gen>:σου
σο<prn><pers><p2><sg><dat>:σοι{'}
σο<prn><pers><p2><sg><dat>:σοι
σο<prn><pers><p2><sg><acc>:σε{'}
σο<prn><pers><p2><sg><acc>:σε

# TODO: long υ
υμε<prn><pers><p2><pl><nom>:ὑμει{~}ς
υμε<prn><pers><p2><pl><gen>:ὑμω{~}ν
υμε<prn><pers><p2><pl><dat>:ὑμι{+long}{~}ν
υμε<prn><pers><p2><pl><acc>:ὑμα{+long}{~}ς

LEXICON Negation
μη<adv>:μη{'}
ουκ<adv>:οὐ
ουκ<adv>:οὐκ
ουκ<adv>:οὐχ

PATTERN Demonstratives
[οδε:] DetPrn [<prx><m>:] ProxDem(1): :ProxDem(2) [:δε]
[οδε:] DetPrn [<prx><f>:] ProxDem(1): :ProxDem(3) [:δε]
[οδε:] DetPrn [<prx><nt>:] ProxDem(1): :ProxDem(4) [:δε]
[ουτος:] DetPrn [<med><m>:] MedDem(1): :MedDem(2)
[ουτος:] DetPrn [<med><f>:] MedDem(1): :MedDem(3)
[ουτος:] DetPrn [<med><nt>:] MedDem(1): :MedDem(4)
[εκεινος:] DetPrn [<dst><m>:ἐκει{'}ν] DistDem(1): :DistDem(2)
[εκεινος:] DetPrn [<dst><f>:ἐκει{'}ν] DistDem(1): :DistDem(3)
[εκεινος:] DetPrn [<dst><nt>:ἐκει{'}ν] DistDem(1): :DistDem(4)

LEXICON DetPrn
<det><dem>:
<prn><dem>:

LEXICON ProxDem(4)
<sg><nom> ὁ{'}   ἡ{'}   το{'}
<sg><gen> του{~} τη{~}  του{~}
<sg><dat> τῳ{~}  τῃ{~}  τῳ{~}
<sg><acc> το{'}ν τη{'}ν το{'}

<pl><nom> οἱ{'}   αἱ{'}         τα{'}
<pl><gen> τω{~}ν  τω{~}ν        τω{~}ν
<pl><dat> τοι{~}σ ται{~}σ       τοι{~}σ
<pl><acc> του{'}σ τα{+long}{'}σ τα{'}

LEXICON MedDem(4)
<sg><nom> οὑ{~}τος  αὑ{'}τη   του{~}το
<sg><gen> του{'}του ταυ{'}της του{'}του
<sg><dat> του{'}τῳ  ταυ{'}τῃ  του{'}τῳ
<sg><acc> του{~}τον ταυ{'}την του{~}το

<pl><nom> οὑ{~}τοι   αὑ{~}ται         ταυ{~}τα
<pl><gen> του{'}των  του{'}των        του{'}των
<pl><dat> του{'}τοις ταυ{'}ταις       του{'}τοις
<pl><acc> του{'}τους ταυ{'}τα{+long}ς ταυ{~}τα

LEXICON DistDem(4)
<sg><nom> ος η  ο
<sg><gen> ου ης ου
<sg><dat> ῳ  ῃ  ῳ
<sg><acc> ον ην ο

<pl><nom> οι  αι        α
<pl><gen> ων  ων        ων
<pl><dat> οις αις       οις
<pl><acc> ους α{+long}ς α

LEXICON Punctuation
,<cm>:,
.<sent>:.
;<sent>:;
·<sent>:·

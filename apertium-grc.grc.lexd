# Morphological Transducer for Ancient Greek

# {ε} verb past tense augment
# {+long} lengthening
# {σ} disappearing sigma
# {(ν)} movable nu
# {'} stress location (assign stress to vowel preceding last of these)
# {~} stress location, circumflex if possible
# {'?} {~?} potential stress locations in noun suffixes
# {'$} final symbol which enables {'?} and {~?}

##################################################
# NOUNS
##################################################

# TODO: organize

PATTERNS
NOUN
VERB
Adjectives
Copula
Determiners
RelativePronoun
Conjunctions
Prepositions
PersonalPronouns
Negation
Adverbs
Demonstratives
Punctuation

PATTERN NOUN
NounStem[ος,ου,η] [<n><f>:] CaseNumber[ος,ου,η]
NounStem[ος,ου,ο,'$] [<n><m>:] CaseNumber[ος,ου,ο,'$]
NounStem[ος,ου,ο,-'$] [<n><m>:] CaseNumber[ος,ου,ο,-'$]
NounStem[ο,ου,το,'$] [<n><nt>:] CaseNumber[ο,ου,το,'$]

ProperNouns[indec,sg] CaseNumber[indec,sg]
ProperNouns[ος,ου,ο,'$,sg] CaseNumber[ος,ου,ο,'$,sg]

IrregProperNouns

NounStem[ᾱ,ᾱς,η,'$] [<n><f>:] (Dec1Sg(1): :Dec1Sg(2)) | Dec1Pl [:{'$}]
NounStem[ᾱ,ᾱς,η,-'$] [<n><f>:] (Dec1Sg(1): :Dec1Sg(2)) | Dec1Pl
NounStem[η,ης,η,'$] [<n><f>:] (Dec1Sg(1): :Dec1Sg(3)) | Dec1Pl [:{'$}]
NounStem[η,ης,η,-'$] [<n><f>:] (Dec1Sg(1): :Dec1Sg(3)) | Dec1Pl
NounStem[α,ης,η,'$] [<n><f>:] (Dec1Sg(1): :Dec1Sg(4)) | Dec1Pl [:{'$}]
NounStem[α,ης,η,-'$] [<n><f>:] (Dec1Sg(1): :Dec1Sg(4)) | Dec1Pl
NounStem[α,ας,η,'$] [<n><f>:] (Dec1Sg(1): :Dec1Sg(5)) | Dec1Pl [:{'$}]
NounStem[α,ας,η,-'$] [<n><f>:] (Dec1Sg(1): :Dec1Sg(5)) | Dec1Pl
NounStem[ης,ου,ο,'$] [<n><m>:] (Dec1Sg(1): :Dec1Sg(6)) | Dec1Pl [:{'$}]
NounStem[ης,ου,ο,-'$] [<n><m>:] (Dec1Sg(1): :Dec1Sg(6)) | Dec1Pl
NounStem[ᾱς,ου,ο,'$] [<n><m>:] (Dec1Sg(1): :Dec1Sg(7)) | Dec1Pl [:{'$}]
NounStem[ᾱς,ου,ο,-'$] [<n><m>:] (Dec1Sg(1): :Dec1Sg(7)) | Dec1Pl

LEXICON CaseNumber
<nom><sg>:ος[ος,ου,η]

<nom><sg>:ο{'}ς[ος,ου,ο,'$,sg]
<gen><sg>:ου{~}[ος,ου,ο,'$,sg]
<dat><sg>:ῳ{~}[ος,ου,ο,'$,sg]
<acc><sg>:ο{'}ν[ος,ου,ο,'$,sg]

<nom><pl>:οι{'}[ος,ου,ο,'$]
<gen><pl>:ω{~}ν[ος,ου,ο,'$]
<dat><pl>:οι{~}ς[ος,ου,ο,'$]
<acc><pl>:ου{'}ς[ος,ου,ο,'$]

<nom><sg>:ος[ος,ου,ο]
<gen><sg>:ου[ος,ου,ο]
<dat><sg>:ῳ[ος,ου,ο]
<acc><sg>:ον[ος,ου,ο]

<nom><pl>:οι[ος,ου,ο]
<gen><pl>:ων[ος,ου,ο]
<dat><pl>:οι[ος,ου,ο]
<acc><pl>:ους[ος,ου,ο]

<nom><sg>:ο{'}[ο,ου,το,'$]
<gen><sg>:ου{~}[ο,ου,το,'$]
<dat><sg>:ῳ{~}[ο,ου,το,'$]
<acc><sg>:ο{'}[ο,ου,το,'$]
#<voc><sg>:[ο,ου,το,'$]

<nom><pl>:α{'}[ο,ου,το,'$]
<gen><pl>:ω{~}ν[ο,ου,το,'$]
<dat><pl>:οι{~}ς[ο,ου,το,'$]
<acc><pl>:α{'}[ο,ου,το,'$]
#<voc><pl>:[ο,ου,το,'$]

<nom><sg>:[indec,sg]
<gen><sg>:[indec,sg]
<dat><sg>:[indec,sg]
<acc><sg>:[indec,sg]
<voc><sg>:[indec,sg]

LEXICON Dec1Sg(7)
# 2 - Fᾱ
# 3 - Fη
# 4 - Fα/ης
# 5 - Fα/ας
# 6 - Mης
# 7 - Mᾱς

<nom><sg> α{+long}{'?}  η{'?}  α{'?}  α{'?}         η{'?}ς α{+long}{'?}ς
<gen><sg> α{+long}{~?}ς η{~?}ς η{~?}ς α{+long}{~?}ς ου{~?} ου{~?}
<dat><sg> ᾳ{~?}         ῃ{~?}  ῃ{~?}  ᾳ{~?}         ῃ{~?}  ᾳ{~?}
<acc><sg> α{+long}{'?}ν η{'?}ν α{'?}ν α{'?}ν        η{'?}ν α{+long}{'?}ν
<voc><sg> α{+long}{'?}  η{'?}  α{'?}  α{'?}         α{'?}  α{+long}{'?}

LEXICON Dec1Pl
<nom><pl>:αι{'?}
<gen><pl>:ω{~}ν
<dat><pl>:αι{~?}ς
<acc><pl>:α{+long}{'?}ς
<voc><pl>:αι{'?}


LEXICON NounStem
αυτος:αὐτ[ος,ου,ο,'$]
αυτος:αὐτ[η,ης,η,'$]
αυτος:αὐτ[ο,ου,το,'$]
βιβλος:βι{'}βλ[ος,ου,η]
κυριος:κυ{'}ρι[ος,ου,ο]
θεᾱ:θε[ᾱ,ᾱς,η]
θεος:θε[ος,ου,ο,'$]

αγαθοποιος:ἀγαθοποι[ος,ου,ο,'$]
αγγελος:ἀ{'}γγελ[ος,ου,ο]
αγιασμος:ἁγιασμ[ος,ου,ο,'$]
αγνισμος:ἁγνισμ[ος,ου,ο,'$]
αγρος:ἀγρ[ος,ου,ο,'$]
αδελφος:ἀδελφ[ος,ου,ο,'$]
αετος:ἀετ[ος,ου,ο,'$]
αιγιαλος:αἰγιαλ[ος,ου,ο,'$]
αινος:αἰ{'}ν[ος,ου,ο]
αιτιος:αἰ{'}τι[ος,ου,ο]
αιχμαλωτος:αἰχμα{'}λωτ[ος,ου,ο]
αλλοτριοεπισκοπος:ἀλλοτριοεπι{'}σκοπ[ος,ου,ο]
αμνος:ἀμν[ος,ου,ο,'$]
αμπελουργος:ἀμπελουργ[ος,ου,ο,'$]
αναβαθμος:ἀναβαθμ[ος,ου,ο,'$]
ανδροφονος:ἀνδροφο{'}ν[ος,ου,ο]
ανεμος:ἀ{'}νεμ[ος,ου,ο]
ανεψιος:ἀνεψι[ος,ου,ο,'$]
ανθρωποκτονος:ἀνθρωποκτο{'}ν[ος,ου,ο]
ανθρωπος:ἀ{'}νθρωπ[ος,ου,ο]
ανθυπατος:ἀνθυ{'}πατ[ος,ου,ο]
αντιδικος:ἀντι{'}δικ[ος,ου,ο]
αντιχριστος:ἀντι{'}χριστ[ος,ου,ο]
απαρτισμος:ἀπαρτισμ[ος,ου,ο,'$]
απελεγμος:ἀπελεγμ[ος,ου,ο,'$]
απελευθερος:ἀπελευ{'}θερ[ος,ου,ο]
αποστολος:ἀπο{'}στολ[ος,ου,ο]
αργυροκοπος:ἀργυροκο{'}π[ος,ου,ο]
αργυρος:ἀ{'}ργυρ[ος,ου,ο]
αριθμος:ἀριθμ[ος,ου,ο,'$]
αρμος:ἁρμ[ος,ου,ο,'$]
αρπαγμος:ἁρπαγμ[ος,ου,ο,'$]
αρτος:ἀ{'}ρτ[ος,ου,ο]
αρχαγγελος:ἀρχα{'}γγελ[ος,ου,ο]
αρχηγος:ἀρχηγ[ος,ου,ο,'$]
αρχισυναγωγος:ἀρχισυνα{'}γωγ[ος,ου,ο]
αρχιτρικλινος:ἀρχιτρι{'}κλιν[ος,ου,ο]
ασκος:ἀσκ[ος,ου,ο,'$]
ασπασμος:ἀσπασμ[ος,ου,ο,'$]
αυλος:αὐλ[ος,ου,ο,'$]
αφανισμος:ἀφανισμ[ος,ου,ο,'$]
αφρος:ἀφρ[ος,ου,ο,'$]
αψινθος:ἀ{'}ψινθ[ος,ου,ο]
βαθμος:βαθμ[ος,ου,ο,'$]
βαπτισμος:βαπτισμ[ος,ου,ο,'$]
βαρβαρος:βα{'}ρβαρ[ος,ου,ο]
βασανισμος:βασανισμ[ος,ου,ο,'$]
βατος:βα{'}τ[ος,ου,ο]
βατραχος:βα{'}τραχ[ος,ου,ο]
βηρυλλος:βη{'}ρυλλ[ος,ου,ο]
βιος:βι{'}[ος,ου,ο]
βοθυνος:βο{'}θυν[ος,ου,ο]
βορβορος:βο{'}ρβορ[ος,ου,ο]
βουνος:βουν[ος,ου,ο,'$]
βροχος:βρο{'}χ[ος,ου,ο]
βρυγμος:βρυγμ[ος,ου,ο,'$]
βυθος:βυθ[ος,ου,ο,'$]
βωμος:βωμ[ος,ου,ο,'$]
γαμος:γα{'}μ[ος,ου,ο]
γεωργος:γεωργ[ος,ου,ο,'$]
γνοφος:γνο{'}φ[ος,ου,ο]
γογγυσμος:γογγυσμ[ος,ου,ο,'$]
γομος:γο{'}μ[ος,ου,ο]
δακτυλιος:δακτυ{'}λι[ος,ου,ο]
δακτυλος:δα{'}κτυλ[ος,ου,ο]
δεξιολαβος:δεξιο{'}λαβ[ος,ου,ο]
δεσμιος:δε{'}σμι[ος,ου,ο]
δεσμος:δεσμ[ος,ου,ο,'$]
δημιουργος:δημιουργ[ος,ου,ο,'$]
δημος:δη{'}μ[ος,ου,ο]
διαδοχος:δια{'}δοχ[ος,ου,ο]
διαλογισμος:διαλογισμ[ος,ου,ο,'$]
διαμερισμος:διαμερισμ[ος,ου,ο,'$]
διδασκαλος:διδα{'}σκαλ[ος,ου,ο]
διωγμος:διωγμ[ος,ου,ο,'$]
δολος:δο{'}λ[ος,ου,ο]
δουλος:δου{'}λ[ος,ου,ο]
δρομος:δρο{'}μ[ος,ου,ο]
ελεγχος:ἐ{'}λεγχ[ος,ου,ο]
εμπαιγμος:ἐμπαιγμ[ος,ου,ο,'$]
εμπορος:ἐ{'}μπορ[ος,ου,ο]
ενιαυτος:ἐνιαυτ[ος,ου,ο,'$]
ενταφιασμος:ἐνταφιασμ[ος,ου,ο,'$]
επαινος:ἐ{'}παιν[ος,ου,ο]
επισιτισμος:ἐπισιτισμ[ος,ου,ο,'$]
επισκοπος:ἐπι{'}σκοπ[ος,ου,ο]
επιτροπος:ἐπι{'}τροπ[ος,ου,ο]
εριφος:ἐ{'}ριφ[ος,ου,ο]
εταιρος:ἑται{'}ρ[ος,ου,ο]
ευνουχος:εὐνου{'}χ[ος,ου,ο]
ζηλος:ζη{'}λ[ος,ου,ο]
ζοφος:ζο{'}φ[ος,ου,ο]
ζυγος:ζυγ[ος,ου,ο,'$]
ηλιος:ἡ{'}λι[ος,ου,ο]
ηλος:ἡ{'}λ[ος,ου,ο]
ηχος:ἠ{'}χ[ος,ου,ο]
θανατος:θα{'}νατ[ος,ου,ο]
θεος:θε[ος,ου,ο,'$]
θερισμος:θερισμ[ος,ου,ο,'$]
θησαυρος:θησαυρ[ος,ου,ο,'$]
θορυβος:θο{'}ρυβ[ος,ου,ο]
θρηνος:θρη{'}ν[ος,ου,ο]
θρομβος:θρο{'}μβ[ος,ου,ο]
θρονος:θρο{'}ν[ος,ου,ο]
θυμος:θυμ[ος,ου,ο,'$]
θυρεος:θυρε[ος,ου,ο,'$]
ιατρος:ἰατρ[ος,ου,ο,'$]
ιλασμος:ἱλασμ[ος,ου,ο,'$]
ιματισμος:ἱματισμ[ος,ου,ο,'$]
ιος:ἰ[ος,ου,ο,'$]
ιππος:ἱ{'}ππ[ος,ου,ο]
καθαρισμος:καθαρισμ[ος,ου,ο,'$]
καιρος:καιρ[ος,ου,ο,'$]
καλαμος:κα{'}λαμ[ος,ου,ο]
καπνος:καπν[ος,ου,ο,'$]
καρπος:καρπ[ος,ου,ο,'$]
κατακλυσμος:κατακλυσμ[ος,ου,ο,'$]
καταρτισμος:καταρτισμ[ος,ου,ο,'$]
κατασκοπος:κατα{'}σκοπ[ος,ου,ο]
κατηγορος:κατη{'}γορ[ος,ου,ο]
κεραμος:κε{'}ραμ[ος,ου,ο]
κηνσος:κη{'}νσ[ος,ου,ο]
κηπος:κη{'}π[ος,ου,ο]
κηπουρος:κηπουρ[ος,ου,ο,'$]
κιθαρωδος:κιθαρω{'}δ[ος,ου,ο,'$]
κινδυνος:κι{'}νδυν[ος,ου,ο]
κλαδος:κλα{'}δ[ος,ου,ο]
κλαυθμος:κλαυθμ[ος,ου,ο,'$]
κληρονομος:κληρονο{'}μ[ος,ου,ο]
κληρος:κλη{'}ρ[ος,ου,ο]
κλιβανος:κλι{'}βαν[ος,ου,ο]
κοκκος:κο{'}κκ[ος,ου,ο]
κολπος:κο{'}λπ[ος,ου,ο]
κονιορτος:κονιορτ[ος,ου,ο,'$]
κοπετος:κοπετ[ος,ου,ο,'$]
κοπος:κο{'}π[ος,ου,ο]
κορος:κο{'}ρ[ος,ου,ο]
κοσμος:κο{'}σμ[ος,ου,ο]
κοφινος:κο{'}φιν[ος,ου,ο]
κραβαττος:κρα{'}βαττ[ος,ου,ο]
κρημνος:κρημν[ος,ου,ο,'$]
κρυσταλλος:κρυ{'}σταλλ[ος,ου,ο]
κυκλος:κυ{'}κλ[ος,ου,ο]
κυριος:κυ{'}ρι[ος,ου,ο]
κωμος:κω{'}μ[ος,ου,ο]
λαος:λα[ος,ου,ο,'$]
λειτουργος:λειτουργ[ος,ου,ο,'$]
λεπρος:λεπρ[ος,ου,ο,'$]
ληρος:λη{'}ρ[ος,ου,ο]
λιβανος:λι{'}βαν[ος,ου,ο]
λιβανωτος:λιβανωτ[ος,ου,ο,'$]
λιθος:λι{'}θ[ος,ου,ο]
λογισμος:λογισμ[ος,ου,ο,'$]
λογος:λο{'}γ[ος,ου,ο]
λοιδορος:λοι{'}δορ[ος,ου,ο]
λοιμος:λοιμ[ος,ου,ο,'$]
λυκος:λυ{'}κ[ος,ου,ο]
λυχνος:λυ{'}χν[ος,ου,ο]
μαγος:μα{'}γ[ος,ου,ο]
μακαρισμος:μακαρισμ[ος,ου,ο,'$]
μαστος:μαστ[ος,ου,ο,'$]
ματαιολογος:ματαιολο{'}γ[ος,ου,ο]
μεθυσος:με{'}θυσ[ος,ου,ο]
μερισμος:μερισμ[ος,ου,ο,'$]
μετοχος:με{'}τοχ[ος,ου,ο]
μηρος:μηρ[ος,ου,ο,'$]
μιασμος:μιασμ[ος,ου,ο,'$]
μισθιος:μι{'}σθι[ος,ου,ο]
μισθος:μισθ[ος,ου,ο,'$]
μισθωτος:μισθωτ[ος,ου,ο,'$]
μογγιλαλος:μογγιλα{'}λ[ος,ου,ο]
μοδιος:μο{'}δι[ος,ου,ο]
μοιχος:μοιχ[ος,ου,ο,'$]
μολυσμος:μολυσμ[ος,ου,ο,'$]
μοχθος:μο{'}χθ[ος,ου,ο]
μυελος:μυελ[ος,ου,ο,'$]
μυθος:μυ{'}θ[ος,ου,ο]
μυλος:μυ{'}λ[ος,ου,ο]
μωμος:μω{'}μ[ος,ου,ο]
ναος:να[ος,ου,ο,'$]
ναυκληρος:ναυ{'}κληρ[ος,ου,ο]
νεανισκος:νεανι{'}σκ[ος,ου,ο]
νεοσσος:νεοσσ[ος,ου,ο,'$]
νεφρος:νεφρ[ος,ου,ο,'$]
νομοδιδασκαλος:νομοδιδα{'}σκαλ[ος,ου,ο]
νομος:νο{'}μ[ος,ου,ο]
νοτος:νο{'}τ[ος,ου,ο]
νυμφιος:νυμφι{'}[ος,ου,ο]
νωτος:νω{'}τ[ος,ου,ο]
ογκος:ὀ{'}γκ[ος,ου,ο]
οδηγος:ὁδηγ[ος,ου,ο,'$]
οδυρμος:ὀδυρμ[ος,ου,ο,'$]
οικονομος:οἰκονο{'}μ[ος,ου,ο]
οικος:οἰ{'}κ[ος,ου,ο]
οικτιρμος:οἰκτιρμ[ος,ου,ο,'$]
οινος:οἰ{'}ν[ος,ου,ο]
ολεθρος:ὀ{'}λεθρ[ος,ου,ο]
ολυνθος:ὀ{'}λυνθ[ος,ου,ο]
ομβρος:ὀ{'}μβρ[ος,ου,ο]
ονειδισμος:ὀνειδισμ[ος,ου,ο,'$]
ορθρος:ὀ{'}ρθρ[ος,ου,ο]
ορκος:ὁ{'}ρκ[ος,ου,ο]
ουρανος:οὐραν[ος,ου,ο,'$]
οφθαλμος:ὀφθαλμ[ος,ου,ο,'$]
οχλος:ὀ{'}χλ[ος,ου,ο]
παιδαγωγος:παιδαγωγ[ος,ου,ο,'$]
παραδεισος:παρα{'}δεισ[ος,ου,ο]
παρακλητος:παρα{'}κλητ[ος,ου,ο]
παραπικρασμος:παραπικρασμ[ος,ου,ο,'$]
παροξυσμος:παροξυσμ[ος,ου,ο,'$]
παροργισμος:παροργισμ[ος,ου,ο,'$]
πειρασμος:πειρασμ[ος,ου,ο,'$]
πενθερος:πενθερ[ος,ου,ο,'$]
πηλος:πηλ[ος,ου,ο,'$]
πλουτος:πλου{'}τ[ος,ου,ο]
πολεμος:πο{'}λεμ[ος,ου,ο]
πονος:πο{'}ν[ος,ου,ο]
πορισμος:πορισμ[ος,ου,ο,'$]
πορνος:πο{'}ρν[ος,ου,ο]
ποταμος:ποταμ[ος,ου,ο,'$]
ποτος:πο{'}τ[ος,ου,ο]
προγονος:προ{'}γον[ος,ου,ο]
προσηλυτος:προση{'}λυτ[ος,ου,ο]
πυργος:πυ{'}ργ[ος,ου,ο]
πυρετος:πυρετ[ος,ου,ο,'$]
πωλος:πω{'}λ[ος,ου,ο]
ραβδουχος:ῥαβδου{'}χ[ος,ου,ο]
ραντισμος:ῥαντισμ[ος,ου,ο,'$]
ρυπος:ῥυ{'}π[ος,ου,ο]
σαββατισμος:σαββατισμ[ος,ου,ο,'$]
σακκος:σα{'}κκ[ος,ου,ο]
σαλος:σα{'}λ[ος,ου,ο]
σεισμος:σεισμ[ος,ου,ο,'$]
σιδηρος:σι{'}δηρ[ος,ου,ο]
σικαριος:σικα{'}ρι[ος,ου,ο]
σιτος:σι{'}τ[ος,ου,ο]
σκηνοποιος:σκηνοποι[ος,ου,ο,'$]
σκοπος:σκοπ[ος,ου,ο,'$]
σκορπιος:σκορπι{'}[ος,ου,ο]
σμαραγδος:σμα{'}ραγδ[ος,ου,ο]
σπερμολογος:σπερμολο{'}γ[ος,ου,ο]
σπιλος:σπι{'}λ[ος,ου,ο]
σπογγος:σπο{'}γγ[ος,ου,ο]
σπορος:σπο{'}ρ[ος,ου,ο]
σταυρος:σταυρ[ος,ου,ο,'$]
στεναγμος:στεναγμ[ος,ου,ο,'$]
στεφανος:στε{'}φαν[ος,ου,ο]
στηριγμος:στηριγμ[ος,ου,ο,'$]
στομαχος:στο{'}μαχ[ος,ου,ο]
στρατηγος:στρατηγ[ος,ου,ο,'$]
στυλος:στυ{'}λ[ος,ου,ο]
συζυγος:συ{'}ζυγ[ος,ου,ο]
συμβουλος:συ{'}μβουλ[ος,ου,ο]
συμπρεσβυτερος:συμπρεσβυ{'}τερ[ος,ου,ο]
συναιχμαλωτος:συναιχμα{'}λωτ[ος,ου,ο]
συνδεσμος:συ{'}νδεσμ[ος,ου,ο]
συνδουλος:συ{'}νδουλ[ος,ου,ο]
συνεργος:συνεργ[ος,ου,ο,'$]
συντροφος:συ{'}ντροφ[ος,ου,ο]
σωφρονισμος:σωφρονισμ[ος,ου,ο,'$]
ταραχος:τα{'}ραχ[ος,ου,ο]
ταυρος:ταυ{'}ρ[ος,ου,ο]
ταφος:τα{'}φ[ος,ου,ο]
τιτλος:τι{'}τλ[ος,ου,ο]
τοιχος:τοι{'}χ[ος,ου,ο]
τοκος:το{'}κ[ος,ου,ο]
τοπος:το{'}π[ος,ου,ο]
τραγος:τρα{'}γ[ος,ου,ο]
τραχηλος:τρα{'}χηλ[ος,ου,ο]
τριβολος:τρι{'}βολ[ος,ου,ο]
τρομος:τρο{'}μ[ος,ου,ο]
τροπος:τρο{'}π[ος,ου,ο]
τροχος:τροχ[ος,ου,ο,'$]
τυπος:τυ{'}π[ος,ου,ο]
υακινθος:ὑα{'}κινθ[ος,ου,ο]
υετος:ὑετ[ος,ου,ο,'$]
υιος:υἱ[ος,ου,ο,'$]
υμνος:ὑ{'}μν[ος,ου,ο]
υπνος:ὑ{'}πν[ος,ου,ο]
υπογραμμος:ὑπογραμμ[ος,ου,ο,'$]
φαγος:φα{'}γ[ος,ου,ο]
φανος:φαν[ος,ου,ο,'$]
φαρμακος:φα{'}ρμακ[ος,ου,ο]
φθογγος:φθο{'}γγ[ος,ου,ο]
φθονος:φθο{'}ν[ος,ου,ο]
φιλοσοφος:φιλο{'}σοφ[ος,ου,ο]
φοβος:φο{'}β[ος,ου,ο]
φονος:φο{'}ν[ος,ου,ο]
φορος:φο{'}ρ[ος,ου,ο]
φραγμος:φραγμ[ος,ου,ο,'$]
φωλεος:φωλε[ος,ου,ο,'$]
φωτισμος:φωτισμ[ος,ου,ο,'$]
χαλινος:χαλιν[ος,ου,ο,'$]
χαλκος:χαλκ[ος,ου,ο,'$]
χειμαρρος:χει{'}μαρρ[ος,ου,ο]
χειραγωγος:χειραγωγ[ος,ου,ο,'$]
χιλιαρχος:χιλι{'}αρχ[ος,ου,ο]
χοιρος:χοι{'}ρ[ος,ου,ο]
χορος:χορ[ος,ου,ο,'$]
χορτος:χο{'}ρτ[ος,ου,ο]
χρηματισμος:χρηματισμ[ος,ου,ο,'$]
χρονος:χρο{'}ν[ος,ου,ο]
χρυσολιθος:χρυσο{'}λιθ[ος,ου,ο]
χρυσοπρασος:χρυσο{'}πρασ[ος,ου,ο]
χρυσος:χρυσ[ος,ου,ο,'$]
χωρος:χω{'}ρ[ος,ου,ο]
ψαλμος:ψαλμ[ος,ου,ο,'$]
ψευδαδελφος:ψευδα{'}δελφ[ος,ου,ο]
ψευδαποστολος:ψευδαπο{'}στολ[ος,ου,ο]
ψευδοδιδασκαλος:ψευδοδιδα{'}σκαλ[ος,ου,ο]
ψευδοχριστος:ψευδο{'}χριστ[ος,ου,ο]
ψιθυρισμος:ψιθυρισμ[ος,ου,ο,'$]
ωμος:ὠ{'}μ[ος,ου,ο]


LEXICON ProperNouns
Δαβιδ<np><ant><m>:Δαβι{'}δ[indec,sg]
Χριστος<np><ant><m>:Χριστ[ος,ου,ο,'$,sg] # TODO: check tags

LEXICON IrregProperNouns
Ιησους<np><ant><m><sg><nom>:Ἰησου{~}ς
Ιησους<np><ant><m><sg><gen>:Ἰησου{~}
Ιησους<np><ant><m><sg><dat>:Ἰησου{~}
Ιησους<np><ant><m><sg><acc>:Ἰησου{~}ν
Ιησους<np><ant><m><sg><voc>:Ἰησου{~}

PATTERN Adjectives
[πας<adj><m>:πα{'}ντ] AdjEndings(1): :AdjEndings(2)
[πας<adj><f>:πα{+long}{'}σ] AdjEndings(1): :AdjEndings(3)
[πας<adj><nt>:πα{'}ντ] AdjEndings(1): :AdjEndings(4)

LEXICON AdjEndings(4)
<nom><sg> ς     α  :
<gen><sg> ο{'}ς ης ο{'}ς
<dat><sg> ι{'}  ῃ  ι{'}
<acc><sg> α     αν :

<nom><pl> ες      αι        α
<gen><pl> ων      ω{~}ν     ων
<dat><pl> σι{(ν)} αις       σι{(ν)}
<acc><pl> ας      α{+long}ς α

##################################################
# Ω VERBS
##################################################

PATTERN VERB
[:{'}] VerbStem1 [<v><ind><actv><impf><pres>:] (ThematicVowelInd VerbPrimary1)[^[1sg,2sg,3sg,1pl,2pl,3pl]]
[:{'}] VerbStem2 [<v><ind><actv><impf><fut>:] (ThematicVowelInd VerbPrimary1)[^[1sg,2sg,3sg,1pl,2pl,3pl]] # CHECK aspect
[:{'}] VerbStemAug1 [<v><ind><actv><impf><past>:] (ThematicVowelInd VerbSecondary1)[^[1sg,2sg,3sg,1pl,2pl,3pl]]
[:{'}] VerbStem1 [<v><ind>:] MP [<impf><pres>:] (ThematicVowelInd VerbPrimary2)[^[1sg,2sg,3sg,1pl,2pl,3pl]]
[:{'}] VerbStem2 [<v><ind><midv><impf><fut>:] (ThematicVowelInd VerbPrimary2)[^[1sg,2sg,3sg,1pl,2pl,3pl]] # CHECK aspect
[:{'}] VerbStemAug1 [<v><ind>:] MP [<impf><past>:] (ThematicVowelInd VerbPrimary2)[^[1sg,2sg,3sg,1pl,2pl,3pl]]
[:{'}] VerbStemAug3[aor2] [<v><ind><actv><aor><past>:] (ThematicVowelInd VerbSecondary1)[^[1sg,2sg,3sg,1pl,2pl,3pl]]

VerbStem1 [<v><ind><actv><impf><inf>:ειν]
VerbStem1 [<v><ind><pass><impf><inf>:εσθαι]
VerbStem2 [<v><ind><actv><impf><inf>:ειν]
VerbStem2 [<v><ind><midv><impf><inf>:εσθαι]

LEXICON MP
<midv>:
<pass>:

PATTERN VerbStem1
VerbRootRegular

PATTERN VerbStemAug1
[:{ε}] VerbRootRegular

PATTERN VerbStem2
VerbRootRegular [:σ]

PATTERN VerbStem3
VerbRootRegular[aor1] [:σ]
VerbRootRegular[aor2]
VerbRootIrreg[pp3]

PATTERN VerbStemAug3
[:{ε}] VerbStem3

LEXICON VerbPrimary1
[1sg]<p1><sg>:{+long}
[2sg]<p2><sg>:{σ}ισ
[3sg]<p3><sg>:{σ}ι
[1pl]<p1><pl>:μεν
[2pl]<p2><pl>:τε
[3pl]<p3><pl>:{+long}σι{(ν)}

LEXICON VerbImperative1
[2sgi]<p2><sg>:
[3sgi]<p3><sg>:τω
[2pli]<p2><pl>:τε
[3pli]<p3><pl>:ντων

LEXICON VerbSecondary1
[1sg]<p1><sg>:μ
[2sg]<p2><sg>:σ
[3sg]<p3><sg>:{(ν)}
[1pl]<p1><pl>:μεν
[2pl]<p2><pl>:τε
[3pl]<p3><pl>:ντ # τ always drops?

LEXICON VerbPrimary2
[1sg]<p1><sg>:μαι
[2sg]<p2><sg>:{σ}αι
[3sg]<p3><sg>:ται
[1pl]<p1><pl>:μεθα
[2pl]<p2><pl>:σθε
[3pl]<p3><pl>:νται

LEXICON VerbSecondary2
[1sg]<p1><sg>:μην
[2sg]<p2><sg>:{σ}ο
[3sg]<p3><sg>:το
[1pl]<p1><pl>:μεθα
[2pl]<p2><pl>:σθε
[3pl]<p3><pl>:ντο

LEXICON ThematicVowelInd
:ο[1sg]
:ο[1pl]
:ο[3pl]
:ε[2sg]
:ε[3sg]
:ε[2pl]

LEXICON ThematicVowelImp
:ο[3pli]
:ε[2sgi]
:ε[2pli]
:ε[3sgi]

LEXICON VerbRootRegular
λεγω:λεγ
παιδευω:παιδευ

LEXICON VerbRootIrreg
λεγω:επ[pp3,aor2]

##################################################
# ΜΙ VERBS
##################################################

LEXICON MILengthening
:{+long}[1sg]
:{+long}[2sg]
:{+long}[3sg]
:[1pl]
:[2pl]
:[3pl]

LEXICON MIEndings(6)
# 1 = pres act ind
# 2 = impf act ind, 2nd aor act ind
# 3 = 1st aor act ind
# 4 = pres mid/pass ind
# 5 = impf mid/pass ind
[1sg]<p1><sg>:μι      :ον  :κα      :μαι  :μην  :μην
[2sg]<p2><sg>:σ       :εσ  :κασ     :σαι  :σο   :{σ}ο
[3sg]<p3><sg>:σι{(ν)} :ε   :κε{(ν)} :ται  :το   :το
[1pl]<p1><pl>:μεν     :μεν :μεν     :μεθα :μεθα :μεθα
[2pl]<p2><pl>:τε      :τε  :τε      :σθε  :σθε  :σθε
[3pl]<p3><pl>:σι{(ν)} :σαν :σαν     :νται :ντο  :ντο

PATTERNS
[:{'}{redup}ι] MIVerbRoot [<v><ind><actv><impf><pres>:] (MILengthening MIEndings(1))[^[1sg,2sg,3sg,1pl,2pl,3pl]]
[:{'}{ε}{redup}ι] MIVerbRoot [<v><ind><actv><impf><past>:] (MILengthening MIEndings(1): :MIEndings(2))[^[1sg,2sg,3sg,1pl,2pl,3pl]]
[:{'}{ε}] MIVerbRoot[1aor] [<v><ind><actv><aor><past>:] (MILengthening MIEndings(1): :MIEndings(3))[^[1sg,2sg,3sg,1pl,2pl,3pl]]
[:{'}{ε}] MIVerbRoot[2aor] [<v><ind><actv><aor><past>:{+long}] (MIEndings(1): :MIEndings(2))[^[1sg,2sg,3sg,1pl,2pl,3pl]]
[:{'}{redup}ι] MIVerbRoot [<v><ind>:] MP [<impf><pres>:] VerbPrimary2
[:{'}{redup}ι] MIVerbRoot [<v><ind>:] MP [<impf><past>:] (MIEndings(1): :MIEndings(4))[^[1sg,2sg,3sg,1pl,2pl,3pl]]
[:{'}{ε}] MIVerbRoot[1aor] [<v><ind><midv><aor><past>:] VerbSecondary2
[:{'}{redup}ι] MIVerbRoot [<v><sub><actv><impf><pres>:{+long}] (ThematicVowelInd VerbPrimary1)[^[1sg,2sg,3sg,1pl,2pl,3pl]]
[:{'}] MIVerbRoot [<v><sub><actv><aor><past>:{+long}] (ThematicVowelInd VerbPrimary1)[^[1sg,2sg,3sg,1pl,2pl,3pl]]
[:{'}{redup}ι] MIVerbRoot [<v><sub>:] MP [<impf><pres>:{+long}] (ThematicVowelInd VerbPrimary2)[^[1sg,2sg,3sg,1pl,2pl,3pl]]

LEXICON MIVerbRoot
διδωμι:δο[1aor]
ιστημι:στα[2aor]

LEXICON Copula
ειμι<vbser><ind><actv><impf><pres><p3><sg>:ἐστιν

LEXICON Determiners
τ<det><def><m><sg><nom>:ὁ
τ<det><def><m><sg><gen>:του{~}
τ<det><def><m><sg><dat>:τῳ{~}
τ<det><def><m><sg><acc>:το{'}ν
τ<det><def><m><pl><nom>:οἱ
τ<det><def><m><pl><gen>:τω{~}ν
τ<det><def><m><pl><dat>:τοι{~}ς
τ<det><def><m><pl><acc>:του{'}ς

τ<det><def><f><sg><nom>:ἡ
τ<det><def><f><sg><gen>:τη{~}ς
τ<det><def><f><sg><dat>:τῃ{~}
τ<det><def><f><sg><acc>:τη{'}ν
τ<det><def><f><pl><nom>:αἱ
τ<det><def><f><pl><gen>:τω{~}ν
τ<det><def><f><pl><dat>:ται{~}ς
τ<det><def><f><pl><acc>:τα{'}ς # TODO: long

τ<det><def><nt><sg><nom>:το{'}
τ<det><def><nt><sg><gen>:του{~}
τ<det><def><nt><sg><dat>:τῳ{~}
τ<det><def><nt><sg><acc>:το{'}
τ<det><def><nt><pl><nom>:τα{'} # TODO: long
τ<det><def><nt><pl><gen>:τω{~}ν
τ<det><def><nt><pl><dat>:τοι{~}ς
τ<det><def><nt><pl><acc>:τα{'} # TOOD: long

LEXICON RelativePronoun
ο<prn><rel><p3><m><sg><nom>:ὁ{'}ς
ο<prn><rel><p3><m><sg><acc>:ὁ{'}ν

η<prn><rel><p3><f><sg><nom>:ἡ{'}
η<prn><rel><p3><f><sg><acc>:ἡ{'}ν

ο<prn><rel><p3><nt><sg><nom>:ὁ{'}
ο<prn><rel><p3><nt><sg><acc>:ὁ{'}

LEXICON Conjunctions
αλλα<cnjcoo>:ἀλλα{'}
αλλα<cnjcoo>:ἀλλ # Dir/LR TODO: apostrophe?
αν<cnjcoo>:ἀ{'}ν # TODO: check tag
δε<cnjcoo>:δε{'}
ει<cnjcoo>:εἰ # TODO: check tag
γαρ<cnjcoo>:γα{'}ρ
η<cnjcoo>:ἠ{'}
ινα<cnjsub>:ἱ{'}να
και<cnjcoo>:και{'}
μεν<cnjcoo>:με{'}ν # TODO: check tag
οτι<cnjsub>:ὁ{'}τι
ουν<cnjcoo>:οὐ{~}ν  # TODO: check tag
τε<cnjcoo>:τε
ως<cnjsub>:ὡς

ει<cnjcoo>+αν<cnjcoo>:ἐα{+long}{'}ν
ει<cnjcoo>+αν<cnjcoo>:ἠ{'}ν
ει<cnjcoo>+αν<cnjcoo>:ἀ{+long}{'}ν

LEXICON PrepositionEndings
~πι # Dir/RL
πι{'} # Dir/LR
π # Dir/LR
φ # Dir/LR

LEXICON Prepositions # page 383
# TODO: abbreviated forms
αμφι<pr>:ἀμφι{'}
ανα<pr>:ἀνα{'}
αντι<pr>:ἀντι{'}
απο<pr>:ἀπο{'}
δια<pr>:δια{'}
εις<pr>:εἰς
εκ<pr>:ἐκ
εκ<pr>:ἐχ # Dir/LR
εκ<pr>:ἐξ # Dir/LR
εν<pr>:ἐν
επι<pr>:ἐπι{'}
επι<pr>:ἐπ # Dir/LR
επι<pr>:ἐφ # Dir/LR
κατα<pr>:κατα{'}
μετα<pr>:μετα{'}
μετα<pr>:μετ # Dir/LR
μετα<pr>:μεθ # Dir/LR
παρα<pr>:παρα{'}
περι<pr>:περι{'}
προ<pr>:προ{'}
προς<pr>:προ{'}ς
συν<pr>:συ{'}ν
υπερ<pr>:ὑπε{'}ρ
υπο<pr>:ὑπο{'}

LEXICON PersonalPronouns
μο<prn><pers><p1><sg><nom>:ἐγω{'}
μο<prn><pers><p1><sg><gen>:ἐμου{~}
μο<prn><pers><p1><sg><gen>:μου
μο<prn><pers><p1><sg><dat>:ἐμοι{'}
μο<prn><pers><p1><sg><dat>:μοι
μο<prn><pers><p1><sg><acc>:ἐμε{'}
μο<prn><pers><p1><sg><acc>:με

ημε<prn><pers><p1><pl><nom>:ἡμει{~}ς
ημε<prn><pers><p1><pl><gen>:ἡμω{~}ν
ημε<prn><pers><p1><pl><dat>:ἡμι{+long}{~}ν
ημε<prn><pers><p1><pl><acc>:ἡμα{+long}{~}ς

σο<prn><pers><p2><sg><nom>:συ{'}
σο<prn><pers><p2><sg><gen>:σου{~}
σο<prn><pers><p2><sg><gen>:σου
σο<prn><pers><p2><sg><dat>:σοι{'}
σο<prn><pers><p2><sg><dat>:σοι
σο<prn><pers><p2><sg><acc>:σε{'}
σο<prn><pers><p2><sg><acc>:σε

# TODO: long υ
υμε<prn><pers><p2><pl><nom>:ὑμει{~}ς
υμε<prn><pers><p2><pl><gen>:ὑμω{~}ν
υμε<prn><pers><p2><pl><dat>:ὑμι{+long}{~}ν
υμε<prn><pers><p2><pl><acc>:ὑμα{+long}{~}ς

LEXICON Negation
μη<adv>:μη{'}
ουκ<adv>:οὐ
ουκ<adv>:οὐκ
ουκ<adv>:οὐχ

LEXICON Adverbs
νυν<adv>:νυ{+long}{~}ν

PATTERN Demonstratives
[οδε:] DetPrn [<prx><m>:] ProxDem(1): :ProxDem(2) [:δε]
[οδε:] DetPrn [<prx><f>:] ProxDem(1): :ProxDem(3) [:δε]
[οδε:] DetPrn [<prx><nt>:] ProxDem(1): :ProxDem(4) [:δε]
[ουτος:] DetPrn [<med><m>:] MedDem(1): :MedDem(2)
[ουτος:] DetPrn [<med><f>:] MedDem(1): :MedDem(3)
[ουτος:] DetPrn [<med><nt>:] MedDem(1): :MedDem(4)
[εκεινος:] DetPrn [<dst><m>:ἐκει{'}ν] DistDem(1): :DistDem(2)
[εκεινος:] DetPrn [<dst><f>:ἐκει{'}ν] DistDem(1): :DistDem(3)
[εκεινος:] DetPrn [<dst><nt>:ἐκει{'}ν] DistDem(1): :DistDem(4)

LEXICON DetPrn
<det><dem>:
<prn><dem>:

LEXICON ProxDem(4)
<sg><nom> ὁ{'}   ἡ{'}   το{'}
<sg><gen> του{~} τη{~}  του{~}
<sg><dat> τῳ{~}  τῃ{~}  τῳ{~}
<sg><acc> το{'}ν τη{'}ν το{'}

<pl><nom> οἱ{'}   αἱ{'}         τα{'}
<pl><gen> τω{~}ν  τω{~}ν        τω{~}ν
<pl><dat> τοι{~}σ ται{~}σ       τοι{~}σ
<pl><acc> του{'}σ τα{+long}{'}σ τα{'}

LEXICON MedDem(4)
<sg><nom> οὑ{~}τος  αὑ{'}τη   του{~}το
<sg><gen> του{'}του ταυ{'}της του{'}του
<sg><dat> του{'}τῳ  ταυ{'}τῃ  του{'}τῳ
<sg><acc> του{~}τον ταυ{'}την του{~}το

<pl><nom> οὑ{~}τοι   αὑ{~}ται         ταυ{~}τα
<pl><gen> του{'}των  του{'}των        του{'}των
<pl><dat> του{'}τοις ταυ{'}ταις       του{'}τοις
<pl><acc> του{'}τους ταυ{'}τα{+long}ς ταυ{~}τα

LEXICON DistDem(4)
<sg><nom> ος η  ο
<sg><gen> ου ης ου
<sg><dat> ῳ  ῃ  ῳ
<sg><acc> ον ην ο

<pl><nom> οι  αι        α
<pl><gen> ων  ων        ων
<pl><dat> οις αις       οις
<pl><acc> ους α{+long}ς α

# TODO: keep this in sync with grc-stress.lexd
LEXICON Punctuation
,<cm>:,
.<sent>:.
;<sent>:;
·<sent>:·

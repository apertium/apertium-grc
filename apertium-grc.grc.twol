Alphabet
 Α Β Γ Δ Ε Ζ Η Θ Ι Κ Λ Μ Ν Ξ Ο Π Ρ Σ Τ Υ Φ Χ Ψ Ω
 α β γ δ ε ζ η θ ι κ λ μ ν ξ ο π ρ σ τ υ φ χ ψ ω
 ς
 ῃ ῳ
 Ἀ Ἐ Ἠ Ἰ Ὀ   Ὠ
 ἀ ἐ ἠ ἰ ὀ ὐ ὠ
 Ἁ Ἑ Ἡ Ἱ Ὁ Ὑ Ὡ
 ἁ ἑ ἡ ἱ ὁ ὑ ὡ
 %{'%} %{%~%} %{%+long%} %{%-long%} ! pass these through to the stress twol file
 %{%+longd%}:0 %{%+longd%}:υ
 %{σ%}:0 %{σ%}:σ
 %{ε%}:ἐ %{ε%}:0
 %{'%?%}:0 %{'%?%}:%{'%} %{%~%?%}:0 %{%~%?%}:%{%~%} %{'%$%}:0
 %{%(ν%)%}:0 %{%(ν%)%}:ν
 %{%+%}:0
 %{redup%}
 ;

Sets

Vowel = Α Ε Η Ι Ο Υ Ω
        α ε η ι ο υ ω
        ῃ ῳ ;

Consonant = Β Γ Δ Ζ Θ Κ Λ Μ Ν Ξ Π Ρ Σ Τ Φ Χ Ψ
            β γ δ ζ θ κ λ μ ν ξ π ρ σ τ φ χ ψ
                                    ς ;

Dental = Τ Δ Θ
         τ δ θ ;

!Definitions

! vowel contractions
!C%_A%_notO = α %{%+%}:0 [ α:%{%+long%} | ε:%{%+long%} | η:%{%+long%} ] ;
!C%_A%_O = α:0 %{%+%}:0 [ ο:ω ( υ:0 ) | ω ] ;
!C%_E%_A = ε:0 %{%+%}:0 [ α:η | η ] ;
!C%_E%_E = ε %{%+%}:0 [ ε:ι | ο:0 ι ] ;
!C%_E%_O = ε:ο %{%+%}:0 ο:υ ;
!C%_E%_OU = ε:0 %{%+%}:0 ο υ ;
!C%_E%_Om = ε:0 %{%+%}:0 ω ;
!C%_O%_AO = ο:0 %{%+%}:0 [ α:ω | η:ω | ω ] ;
!C%_O%_EO = ο %{%+%}:0 [ ε:υ | ο:υ ( υ:0 ) ] ;
!C%_O%_OI = ο %{%+%}:0 [ [ ε:0 | ο:0 ] ι | ῃ:ι ] ;

!Vowel%_Contract = [ C%_A%_notO | C%_A%_O |
!                    C%_E%_A | C%_E%_E | C%_E%_O | C%_E%_OU | C%_E%_Om |
!                    C%_O%_AO | C%_O%_EO | C%_O%_OI ] ;

!A%_E = %{ε%}:ε [ ε:0 ἰ | ἐ:ἰ | ἑ:ἱ ] ;

!Augment = [ A%_E ] ;

Rules

"augment is ε only before consonants"
%{ε%}:ἐ <=> _ :Consonant ;

!"augment vowels"
!<[ Augment ]> <==> _ ;

"aug + ε → εἰ part 1"
%{ε%}:ε <=> _ ε: ;

"aug + ε → εἰ part 2"
ε:ἰ <=> %{ε%}:ε _ ;

"ο {+long} -> ω part 1"
ο:ω <=> _ %{%+long%}: ;

"ο {+long} -> ω part 2"
%{%+long%}:0 <=> ο:ω _ ;

"ο {+longd} -> ου"
%{%+longd%}:υ <=> ο _ ;

"ε {+long} -> η part 1"
ε:η <=> _ %{%+long%}: ;

"ε {+long} -> η part 2"
%{%+long%}:0 <=> ε:η _ ;

"inserting noun acute stress marker"
%{'%?%}:%{'%} <=> _ ?* %{'%$%}:0 ;

"inserting noun circumflex stress marker"
%{%~%?%}:%{%~%} <=> _ ?* %{'%$%}:0 ;

"reduplication"
%{redup%}:Cx <=> _ ε Cx ;
                 where Cx in Consonant ;

"no final μ"
μ:ν <=> _ .#. ;

"no final dental"
Cx:0 <=> _ .#. ;
         where Cx in Dental ;

"{σ} deletes between vowels"
%{σ%}:0 <=> Vowel: _ Vowel: ;

"vowel contraction ε + αι 1"
ε:ῃ <=> _ [ %{σ%}:0 | %{%+%}:0 ] α: ι: ;

"vowel contraction ε + αι 2"
α:0 <=> ε:ῃ [ %{σ%}:0 | %{%+%}:0 ] _ ι: ;

"vowel contraction ε + αι 3"
ι:0 <=> ε:ῃ [ %{σ%}:0 | %{%+%}:0 ] α: _ ;

"final σ"
σ:ς <=> _ .#. ;

! Resources:
! http://wiki.apertium.org/wiki/Starting_a_new_language_with_HFST#Enter_twol
! https://kitwiki.csc.fi/twiki/bin/view/KitWiki/HfstHome
! https://github.com/hfst/
! http://wiki.apertium.org/wiki/Twol
! http://wiki.apertium.org/wiki/Hfst


Alphabet
 Α Β Γ Δ Ε Ζ Η Θ Ι Κ Λ Μ Ν Ξ Ο Π Ρ Σ Τ Υ Φ Χ Ψ Ω
 α β γ δ ε ζ η θ ι κ λ μ ν ξ ο π ρ σ τ υ φ χ ψ ω
 ς
 ῃ ῳ
 Ἀ Ἐ Ἠ Ἰ Ὀ   Ὠ
 ἀ ἐ ἠ ἰ ὀ ὐ ὠ
 Ἁ Ἑ Ἡ Ἱ Ὁ Ὑ Ὡ
 ἁ ἑ ἡ ἱ ὁ ὑ ὡ

 ! pass these through to the stress twol file
 %{'%} %{'%!%} %{%~%} %{%+long%} %{%-long%}

 %{%+longd%}:0 %{%+longd%}:υ %{%+longd%}:%{%+long%}
 %{σ%}:0 %{σ%}:σ
 %{ε%}:ἐ %{ε%}:0
 %{ε%~%}:0
 %{'%?%}:0 %{'%?%}:%{'%} %{%~%?%}:0 %{%~%?%}:%{%~%} %{'%$%}:0
 %{%(ν%)%}:0 %{%(ν%)%}:ν
 %{%+%}
 %{redup%}
 %{%|%}:0
 ;

Sets

Vowel = Α Ε Η Ι Ο Υ Ω
        α ε η ι ο υ ω
        ῃ ῳ ;

Consonant = Β Γ Δ Ζ Θ Κ Λ Μ Ν Ξ Π Ρ Σ Τ Φ Χ Ψ
            β γ δ ζ θ κ λ μ ν ξ π ρ σ τ φ χ ψ
                                    ς ;

Dental = Τ Δ Θ
         τ δ θ ;

!Definitions

! vowel contractions
!C%_A%_notO = α %{%+%}:0 [ α:%{%+long%} | ε:%{%+long%} | η:%{%+long%} ] ;
!C%_A%_O = α:0 %{%+%}:0 [ ο:ω ( υ:0 ) | ω ] ;
!C%_E%_A = ε:0 %{%+%}:0 [ α:η | η ] ;
!C%_E%_E = ε %{%+%}:0 [ ε:ι | ο:0 ι ] ;
!C%_E%_O = ε:ο %{%+%}:0 ο:υ ;
!C%_E%_OU = ε:0 %{%+%}:0 ο υ ;
!C%_E%_Om = ε:0 %{%+%}:0 ω ;
!C%_O%_AO = ο:0 %{%+%}:0 [ α:ω | η:ω | ω ] ;
!C%_O%_EO = ο %{%+%}:0 [ ε:υ | ο:υ ( υ:0 ) ] ;
!C%_O%_OI = ο %{%+%}:0 [ [ ε:0 | ο:0 ] ι | ῃ:ι ] ;

!Vowel%_Contract = [ C%_A%_notO | C%_A%_O |
!                    C%_E%_A | C%_E%_E | C%_E%_O | C%_E%_OU | C%_E%_Om |
!                    C%_O%_AO | C%_O%_EO | C%_O%_OI ] ;

!A%_E = %{ε%}:ε [ ε:0 ἰ | ἐ:ἰ | ἑ:ἱ ] ;

!Augment = [ A%_E ] ;

Rules

"augment is ε only before consonants"
%{ε%}:ἐ <=> _ :Consonant ;
            except _ ?* %{ε%~%}:0 ;

!"augment vowels"
!<[ Augment ]> <==> _ ;

"breathing mark after augment"
ι:ἰ <=> [ %{ε%}:0 | .#. ( %{'%} ) ( α | ε | ο ) ] _ ;

"aug + prep part 1"
%{ε%}:0 <=> _ ?* %{ε%~%}:0 ;

"aug + prep part 2"
ο:ε <=> %{ε%}:0 ?* _ %{ε%~%}:0 ;

"aug + α/ε → η"
ε:ἠ <=> %{ε%}:0 _ ;
        except _ ?* %{ε%~%}:0 ;

"aug + ο → ω"
ο:ὠ <=> %{ε%}:0 _ ;
        except _ ?* %{ε%~%}:0 ;

"ο {+long} -> ω part 1"
ο:ω <=> _ ( %{'%} ) %{%+long%}: ;

"ο {+long} -> ω part 2"
%{%+long%}:0 <=> ο:ω ( %{'%} ) _ ;

"ο {+longd} -> ου"
%{%+longd%}:υ <=> ο _ ;

"ε {+long} -> η part 1"
ε:η <=> ~[ %{ε%}: ] _ ( %{'%} | %{'%?%}: ) %{%+long%}: ;

"ο {+long} -> ω part 1"
ο:ω <=> ~[ %{ε%}: ] _ ( %{'%} | %{'%?%}: ) %{%+long%}: ;

"ε {+long} -> η part 2"
%{%+long%}:0 <=> [ ε:η | ο:ω ] ( %{'%} | %{'%?%}: ) _ ;

"ε' -> ει' after {+longd}"
0:ι <=> ε _ %{'%} %{%+longd%}:0 ;

"ε' -> ει' after {+longd} part 2"
%{%+longd%}:0 <=> 0:ι %{'%} _ ;

"{+longd} -> {+long}"
%{%+longd%}:%{%+long%} <=> α _ ;

"inserting noun acute stress marker"
%{'%?%}:%{'%} <=> _ ?* %{'%$%}:0 ;

"inserting noun circumflex stress marker"
%{%~%?%}:%{%~%} <=> _ ?* %{'%$%}:0 ;

"reduplication"
%{redup%}:Cx <=> _ ε Cx ;
                 where Cx in Consonant ;

"no final μ"
μ:ν <=> _ .#. ;

"no final dental or before σ"
Cx:0 <=> _ .#. ;
         _ ( %{'%?%}: ) [ :σ | :ς ] ;
         where Cx in Dental ;

"{σ} deletes between vowels"
%{σ%}:0 <=> Vowel: _ Vowel: ;

!"vowel contraction ε + αι 1"
!ε:ῃ <=> _ [ %{σ%}:0 | %{%+%}:0 ] α: ι: ;

!"vowel contraction ε + αι 2"
!α:0 <=> ε:ῃ [ %{σ%}:0 | %{%+%}:0 ] _ ι: ;

!"vowel contraction ε + αι 3"
!ι:0 <=> ε:ῃ [ %{σ%}:0 | %{%+%}:0 ] α: _ ;

"final σ"
σ:ς <=> ~[ π: | β: | φ: | κ: | γ: | χ: ] _ .#. ;

"assimilation"
Cx:Cy <=> _ Cz ;
          where Cx in ( π π β β φ φ κ κ γ γ χ χ )
                Cy in ( β φ π φ π β γ χ κ χ κ γ )
                Cz in ( δ θ τ θ τ δ δ θ τ θ τ δ )
          matched ;

"stop before sigma part 1"
Cx:Cy <=> _ σ:0 ;
          where Cx in ( π β φ κ γ χ )
                Cy in ( ψ ψ ψ ξ ξ ξ )
          matched ;

"stop before sigma part 2"
σ:0 <=> Cx:Cy _  ;
          where Cx in ( π β φ κ γ χ )
                Cy in ( ψ ψ ψ ξ ξ ξ )
          matched ;


! Resources:
! http://wiki.apertium.org/wiki/Starting_a_new_language_with_HFST#Enter_twol
! https://kitwiki.csc.fi/twiki/bin/view/KitWiki/HfstHome
! https://github.com/hfst/
! http://wiki.apertium.org/wiki/Twol
! http://wiki.apertium.org/wiki/Hfst

